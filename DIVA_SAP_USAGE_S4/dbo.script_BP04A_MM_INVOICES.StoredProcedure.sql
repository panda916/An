USE [DIVA_SAP_USAGE_S4]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

CREATE      PROCEDURE [dbo].[script_BP04A_MM_INVOICES]
WITH EXECUTE AS CALLER
AS
----DYNAMIC_SCRIPT_START
BEGIN

/*Change history comments*/

/*
	Title			:	BP04A: Invoice Receipt
	  
	--------------------------------------------------------------
	Update history
	--------------------------------------------------------------
	Date		    | Who |	Description
	06/10/2022        KHOA   First version for Sony ECC
*/
/* Initialize parameters from globals table */
 
     DECLARE  
			@CURRENCY NVARCHAR(MAX)                 = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'currency')
			,@DATE1 NVARCHAR(MAX)                           = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'date1')
			,@DATE2 NVARCHAR(MAX)                           = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'date2')
			,@DOWNLOADDATE NVARCHAR(MAX)             = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'downloaddate')
			,@DATEFORMAT VARCHAR(3)             = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'dateformat')
			,@EXCHANGERATETYPE NVARCHAR(MAX)  = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'exchangeratetype')
			,@LANGUAGE1 NVARCHAR(MAX)                = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'language1')
			,@LANGUAGE2 NVARCHAR(MAX)                = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'language2')
			,@YEAR NVARCHAR(MAX)                     = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'year')
			,@ID NVARCHAR(MAX)                       = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'id')
			,@LIMIT_RECORDS INT                    = CAST((SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'LIMIT_RECORDS') AS INT)
			,@ZV_SAME_QUARTER_BY_BLDAT NVARCHAR(MAX) = ISNULL((SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'ZV_SAME_QUARTER_BY_BLDAT'), '')
 


EXEC SP_DROPTABLE 'BP04A_01_TT_A_TCURF'
SELECT 
    TCURF_MANDT,
	TCURF_FCURR,
	TCURF_TCURR,
	CONVERT(DATE,CAST(99999999-TCURF_GDATU AS NVARCHAR), 112) TCURF_GDATU,
	TCURF_FFACT,
	TCURF_TFACT			
INTO BP04A_01_TT_A_TCURF
FROM A_TCURF
WHERE TCURF_KURST = 'M' AND TCURF_GDATU < 90000000


--Step 1/ Create cube for INV
EXEC SP_DROPTABLE 'BP04A_01_IT_INVOICE'

SELECT 
	RBKP_BUKRS,
	AM_SCOPE.SCOPE_BUSINESS_DMN_L1,
	AM_SCOPE.SCOPE_BUSINESS_DMN_L2,
	RBKP_ZLSPR ,
	RBKP_BELNR,
	RBKP_GJAHR,
	RBKP_BLART,
	RBKP_BLDAT,
	RBKP_BUDAT,
	RBKP_USNAM,
	RBKP_TCODE,
	RBKP_CPUDT,
	RBKP_CPUTM,
	RBKP_VGART,
	RBKP_XBLNR,
	RBKP_WAERS,
	RBKP_KURSF,
	RBKP_STBLG,
	RBKP_RBSTAT,
	RSEG_BELNR,
	RSEG_GJAHR,
	RSEG_BUZEI,
	RSEG_EBELN,
	RSEG_EBELP,
	RSEG_ZEKKN,
	RSEG_MATNR,
	RSEG_BWKEY,
	RSEG_BWTAR,
	RSEG_WRBTR,
	RSEG_SHKZG,
	RSEG_MENGE,
	RSEG_BSTME,
	RSEG_PSTYP,
	RBKP_BKTXT,
	RSEG_WERKS,
	T001_BUTXT,
	LFA1_LIFNR,
	LFA1_NAME1,
	LFA1_ERNAM,
	A_T003T.T003T_LTEXT, --Document Type Description
	A_MARA.MARA_MTART, ---Material Type
	A_MARA.MARA_MATKL, --Material Group
	A_T134T.T134T_MTBEZ, --Description of material type
	A_T023T.T023T_WGBEZ, --Material Group Description
	A_MAKT.MAKT_MAKTX ,--Material Description
	V_USERNAME_NAME_TEXT,
	CONVERT(MONEY,RSEG_WRBTR * COALESCE(TCURX_DOC.TCURX_FACTOR,1) * iif(RSEG_SHKZG = 'H', -1, 1)) AS ZF_RSEG_WRBTR_S, --document amount
	CONVERT(MONEY,RSEG_WRBTR * 
	COALESCE(TCURX_CUC.TCURX_FACTOR,1) * 
	RBKP_KURSF * 
		COALESCE(TCURF_COC.TCURF_TFACT,1)/COALESCE(TCURF_COC.TCURF_FFACT,1) *
	COALESCE(CAST(TCURR_CUC.TCURR_UKURS AS FLOAT),1)* 
	COALESCE(TCURF_CUC.TCURF_TFACT,1) / COALESCE(TCURF_CUC.TCURF_FFACT,1))	
	
	AS ZF_RSEG_WRBTR_S_CUC, -- document amount to USD

	CONVERT(MONEY,RSEG_WRBTR * 
	COALESCE(TCURX_CUC.TCURX_FACTOR,1) * 
	RBKP_KURSF * 
		COALESCE(TCURF_COC.TCURF_TFACT,1)/COALESCE(TCURF_COC.TCURF_FFACT,1))	AS ZF_RSEG_WRBTR_S_COC -- document to company currency


INTO BP04A_01_IT_INVOICE
FROM A_RBKP
--Incoming Invoice
LEFT JOIN A_RSEG
ON A_RBKP.RBKP_BUKRS = A_RSEG.RSEG_BUKRS AND A_RBKP.RBKP_BELNR = A_RSEG.RSEG_BELNR AND A_RBKP.RBKP_GJAHR = A_RSEG.RSEG_GJAHR

--Get supplier number, name,...
LEFT JOIN A_LFA1
ON A_LFA1.LFA1_LIFNR = A_RBKP.RBKP_LIFNR
--Get document type
LEFT JOIN A_T003T
ON A_T003T.T003T_BLART = A_RBKP.RBKP_BLART
--Get material type - group
LEFT JOIN A_MARA
ON A_MARA.MARA_MATNR = A_RSEG.RSEG_MATNR
--Get material type - description
LEFT JOIN A_T134T 
ON A_T134T.T134T_MTART = A_MARA.MARA_MTART
--Get material group - desription
LEFT JOIN A_T023T
ON A_T023T.T023T_MATKL = A_MARA.MARA_MATKL
    -- Add the house currency code
LEFT JOIN A_T001
ON  A_RBKP.RBKP_BUKRS = A_T001.T001_BUKRS


  -- Add currency factor from company currency to USD

LEFT JOIN B00_IT_TCURF TCURF_CUC
ON A_T001.T001_WAERS = TCURF_CUC.TCURF_FCURR
AND TCURF_CUC.TCURF_TCURR  = @currency  
AND TCURF_CUC.TCURF_GDATU = (
	SELECT TOP 1 B00_IT_TCURF.TCURF_GDATU
	FROM B00_IT_TCURF
	WHERE A_T001.T001_WAERS = B00_IT_TCURF.TCURF_FCURR AND 
			B00_IT_TCURF.TCURF_TCURR  = @currency  AND
			B00_IT_TCURF.TCURF_GDATU <= RBKP_BUDAT
	ORDER BY B00_IT_TCURF.TCURF_GDATU DESC
	)
-- Add exchange rate from company currency to USD
LEFT JOIN B00_IT_TCURR TCURR_CUC
	ON A_T001.T001_WAERS = TCURR_CUC.TCURR_FCURR
	AND TCURR_CUC.TCURR_TCURR  = @currency  
	AND TCURR_CUC.TCURR_GDATU = (
		SELECT TOP 1 B00_IT_TCURR.TCURR_GDATU
		FROM B00_IT_TCURR
		WHERE A_T001.T001_WAERS = B00_IT_TCURR.TCURR_FCURR AND 
				B00_IT_TCURR.TCURR_TCURR  = @currency  AND
				B00_IT_TCURR.TCURR_GDATU <= RBKP_BUDAT
		ORDER BY B00_IT_TCURR.TCURR_GDATU DESC
		)

LEFT JOIN B00_IT_TCURF TCURF_COC
ON RBKP_WAERS = TCURF_COC.TCURF_FCURR
AND TCURF_COC.TCURF_TCURR  = T001_WAERS  
AND TCURF_COC.TCURF_GDATU = (
	SELECT TOP 1 B00_IT_TCURF.TCURF_GDATU
	FROM B00_IT_TCURF
	WHERE RBKP_WAERS = B00_IT_TCURF.TCURF_FCURR AND 
			B00_IT_TCURF.TCURF_TCURR  = T001_WAERS  AND
			B00_IT_TCURF.TCURF_GDATU <= RBKP_BUDAT
	ORDER BY B00_IT_TCURF.TCURF_GDATU DESC
	)

-- Add currency conversion factor for document currency
LEFT JOIN B00_TCURX TCURX_DOC  
ON RBKP_WAERS = TCURX_DOC.TCURX_CURRKEY
		
-- Add currency conversion factors for company currency
LEFT JOIN B00_TCURX TCURX_CUC      
ON A_T001.T001_WAERS = TCURX_CUC.TCURX_CURRKEY


-- Add the material description 
LEFT JOIN A_MAKT 
ON A_RSEG.RSEG_MATNR = A_MAKT.MAKT_MATNR

-- Add information from the scope table concerning the business domain   
INNER JOIN AM_SCOPE                                         
ON   RBKP_BUKRS = AM_SCOPE.SCOPE_CMPNY_CODE
--Add the payment block key description
LEFT JOIN A_T008T 
ON T008T_ZAHLS=RBKP_ZLSPR AND T008T_SPRAS='EN'
LEFT JOIN A_V_USERNAME
ON RBKP_USNAM=V_USERNAME_BNAME
--/*Drop temporary tables*/

EXEC SP_RENAME_FIELD 'BP04A_01_', 'BP04A_01_IT_INVOICE'


END

GO
