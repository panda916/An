USE [DIVA_SAP_USAGE_S4]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

CREATE          PROCEDURE [dbo].[script_BO04A_SD_INVOICES]
WITH EXECUTE AS CALLER
AS
--DYNAMIC_SCRIPT_START
/* Initiate the log */  
--Create database log table if it does not exist
IF OBJECT_ID('LOG_SP_EXECUTION', 'U') IS NULL BEGIN CREATE TABLE [DBO].[LOG_SP_EXECUTION] ([DATABASE] NVARCHAR(MAX) NULL,[OBJECT] NVARCHAR(MAX) NULL,[OBJECT_TYPE] NVARCHAR(MAX) NULL,[USER] NVARCHAR(MAX) NULL,[DATE] DATE NULL,[TIME] TIME NULL,[DESCRIPTION] NVARCHAR(MAX) NULL,[TABLE] NVARCHAR(MAX),[ROWS] INT) END

--Log start of procedure
INSERT INTO [DBO].[LOG_SP_EXECUTION] ([DATABASE],[OBJECT],[OBJECT_TYPE],[USER],[DATE],[TIME],[DESCRIPTION],[TABLE],[ROWS])
SELECT DB_NAME(),OBJECT_NAME(@@PROCID),'P',SYSTEM_USER,CONVERT(DATE,GETDATE()),CONVERT(TIME,GETDATE()),'Procedure started',NULL,NULL

/* Initialize parameters from globals table */

     DECLARE 	 
			 @CURRENCY NVARCHAR(MAX)			= (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'currency')
			,@DATE1 NVARCHAR(MAX)				= (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'date1')
			,@DATE2 NVARCHAR(MAX)				= (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'date2')
			,@DOWNLOADDATE NVARCHAR(MAX)		= (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'downloaddate')
			,@DATEFORMAT VARCHAR(3)             = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'dateformat')
			,@EXCHANGERATETYPE NVARCHAR(MAX)	= (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'exchangeratetype')
			,@LANGUAGE1 NVARCHAR(MAX)			= (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'language1')
			,@LANGUAGE2 NVARCHAR(MAX)			= (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'language2')
			,@YEAR NVARCHAR(MAX)				= (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'year')
			,@ID NVARCHAR(MAX)					= (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'id')
			,@LIMIT_RECORDS INT		            = CAST((SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'LIMIT_RECORDS') AS INT)


/*Test mode*/

SET ROWCOUNT @LIMIT_RECORDS


/*Change history comments*/

/*Change history comments*/

/*
	Title			:	BO04A: SD INVOICES
	  
	--------------------------------------------------------------
	Update history
	--------------------------------------------------------------
	Date		    | Who |	Description
	12/10/2022        KHOA   First version
*/



-- Step 1: create SD invoices cube
EXEC SP_DROPTABLE 'BO04A_01_IT_SD_INV'

	SELECT
		A_VBRK.VBRK_MANDT,
		A_VBRK.VBRK_FKART,
		AM_SCOPE.SCOPE_BUSINESS_DMN_L1,
		AM_SCOPE.SCOPE_BUSINESS_DMN_L2,
		A_VBRK.VBRK_VBTYP,
		A_VBRK.VBRK_VBELN,
		A_VBRK.VBRK_FKTYP,
		A_VBRP.VBRP_POSNR,
		A_VBRP.VBRP_AUPOS,
		A_VBRP.VBRP_AUBEL,
		A_VBRK.VBRK_AEDAT,
		A_VBRP.VBRP_WERKS,
		A_VBRP.VBRP_FKIMG,
		A_T001W.T001W_NAME1,
		A_T001.T001_BUTXT,
		A_VBRK.VBRK_VKORG,
		A_VBRK.VBRK_KDGRP,
		A_VBRK.VBRK_FKDAT,
		A_VBRK.VBRK_ERDAT,
		A_VBRK.VBRK_BELNR,
		A_VBRK.VBRK_GJAHR,
		A_VBRK.VBRK_BUKRS,
		A_VBRK.VBRK_KUNRG,
		A_VBRK.VBRK_KUNAG,
		A_VBRP.VBRP_VBELN,
		A_VBRK.VBRK_ERNAM,
		A_VBRK.VBRK_WAERK, -- document currency	
		A_T001.T001_BUKRS,
		VBRP_VGBEL,
		VBRP_VGPOS,
		CONVERT(MONEY,(VBRP_NETWR)*ISNULL(TCURX_FACTOR,1)) AS ZF_VBRP_NETWR_S,--document amount
		(VBRP_NETWR)*ISNULL(TCURX_FACTOR,1) * VBRP_KURSK * COALESCE(TCURF_COC.TCURF_TFACT,1)/COALESCE(TCURF_COC.TCURF_FFACT,1) * COALESCE(CAST(TCURR_CUC.TCURR_UKURS AS FLOAT),1) * COALESCE(TCURF_CUC.TCURF_TFACT,1) / COALESCE(TCURF_CUC.TCURF_FFACT,1) ZF_VBRP_NETWR_S_CUC -- Item net value   
	INTO BO04A_01_IT_SD_INV
    
	FROM A_VBRK

	-- Select from PO header/line items
	INNER JOIN A_VBRP
	ON  A_VBRK.VBRK_VBELN = A_VBRP.VBRP_VBELN
	-- Add user account names
	LEFT JOIN A_V_USERNAME 
		ON  A_VBRK.VBRK_ERNAM = A_V_USERNAME.V_USERNAME_BNAME
	-- Add descriptions of plant codes
	LEFT JOIN A_T001W
		ON A_VBRP.VBRP_WERKS = A_T001W.T001W_WERKS  
	-- Add the currency conversion factor: for document currency
	LEFT JOIN A_TCURX TCURX_DOC
		ON 
		 A_VBRK.VBRK_WAERK = TCURX_DOC.TCURX_CURRKEY
   -- Add information from the scope table concerning the business domain   
	INNER JOIN AM_SCOPE                                         
		ON   VBRK_BUKRS = AM_SCOPE.SCOPE_CMPNY_CODE
		--Add company code name
	LEFT JOIN A_T001 
	ON VBRK_BUKRS=T001_BUKRS
	-- Add currency factor from company currency to USD
	LEFT JOIN B00_IT_TCURF TCURF_CUC
	ON A_T001.T001_WAERS = TCURF_CUC.TCURF_FCURR
	AND TCURF_CUC.TCURF_TCURR  = @currency  
	AND TCURF_CUC.TCURF_GDATU = (
		SELECT TOP 1 B00_IT_TCURF.TCURF_GDATU
		FROM B00_IT_TCURF
		WHERE A_T001.T001_WAERS = B00_IT_TCURF.TCURF_FCURR AND 
				B00_IT_TCURF.TCURF_TCURR  = @currency  AND
				B00_IT_TCURF.TCURF_GDATU <= VBRK_ERDAT
		ORDER BY B00_IT_TCURF.TCURF_GDATU DESC
		)
	-- Add exchange rate from company currency to USD
	LEFT JOIN B00_IT_TCURR TCURR_CUC
		ON A_T001.T001_WAERS = TCURR_CUC.TCURR_FCURR
		AND TCURR_CUC.TCURR_TCURR  = @currency  
		AND TCURR_CUC.TCURR_GDATU = (
			SELECT TOP 1 B00_IT_TCURR.TCURR_GDATU
			FROM B00_IT_TCURR
			WHERE A_T001.T001_WAERS = B00_IT_TCURR.TCURR_FCURR AND 
					B00_IT_TCURR.TCURR_TCURR  = @currency  AND
					B00_IT_TCURR.TCURR_GDATU <= VBRK_ERDAT
			ORDER BY B00_IT_TCURR.TCURR_GDATU DESC
			) 
	-- Get currency factor
	LEFT JOIN B00_TCURX
	ON B00_TCURX.TCURX_CURRKEY = VBRK_WAERK

	-- Add currency factor from document currency to local currency
	LEFT JOIN B00_IT_TCURF TCURF_COC
	ON VBRK_WAERK = TCURF_COC.TCURF_FCURR
	AND TCURF_COC.TCURF_TCURR  = T001_WAERS  
	AND TCURF_COC.TCURF_GDATU = (
		SELECT TOP 1 B00_IT_TCURF.TCURF_GDATU
		FROM B00_IT_TCURF
		WHERE VBRK_WAERK = B00_IT_TCURF.TCURF_FCURR AND 
				B00_IT_TCURF.TCURF_TCURR  = T001_WAERS  AND
				B00_IT_TCURF.TCURF_GDATU <= VBRK_ERDAT
		ORDER BY B00_IT_TCURF.TCURF_GDATU DESC
		)

EXEC SP_RENAME_FIELD 'BO04A_01_', 'BO04A_01_IT_SD_INV'

GO
