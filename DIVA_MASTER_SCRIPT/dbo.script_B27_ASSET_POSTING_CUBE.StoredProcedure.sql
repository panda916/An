USE [DIVA_MASTER_SCRIPT]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
-- =============================================
-- Author:		Khoi
-- Create date: <Create Date,,>
-- Description:	Create  asset posting cube
-- =============================================
CREATE     PROCEDURE [dbo].[script_B27_ASSET_POSTING_CUBE]

AS
BEGIN

-- Step 1 Join ANEK (Document Header Asset Posting Table ) and ANEP ( Asset Line Items Table)
EXEC SP_DROPTABLE B27_01_IT_ASSET_POSTING
SELECT DISTINCT A_ANEK.*,
A_ANEP.*,
V_USERNAME_NAME_TEXT,--name of the user who create the asset posting
TSTCT_TTEXT,--TCODE description
MAKT_MAKTG,--material description
MAKT_MAKTX,--material description
TCURR_CUC.TCURR_UKURS AS TCURR_UKURS,
TCURF_CUC.TCURF_TFACT AS TCURF_TFACT,
TCURF_CUC.TCURF_FFACT AS TCURF_FFACT,
T001_WAERS,
COALESCE(CAST(TCURR_CUC.TCURR_UKURS AS FLOAT),1) * COALESCE(TCURF_CUC.TCURF_TFACT,1) / COALESCE(TCURF_CUC.TCURF_FFACT,1) AS EXCHNG_RATIO,--Exchange rate
ANEP_ANBTR* COALESCE(CAST(TCURR_CUC.TCURR_UKURS AS FLOAT),1) * COALESCE(TCURF_CUC.TCURF_TFACT,1) / COALESCE(TCURF_CUC.TCURF_FFACT,1)*COALESCE(TCURX_FACTOR,1) AS ZF_ANEP_ANBTR_CUC, -- posted amount in Custom currency
ANEP_ANBTR*COALESCE(TCURX_FACTOR,1) AS ZF_ANEP_ANBTR --posted amount
INTO B27_01_IT_ASSET_POSTING
FROM A_ANEP JOIN A_ANEK 
ON 
	ANEK_BUKRS=ANEP_BUKRS AND 
	ANEK_ANLN1=ANEP_ANLN1 AND 
	ANEK_ANLN2=ANEP_ANLN2 AND 
	ANEK_GJAHR=ANEP_GJAHR AND 
	ANEK_LNRAN=ANEP_LNRAN
--Add TCODE description
LEFT JOIN A_TSTCT ON ANEK_TCODE=TSTCT_TCODE
--Add material description
LEFT JOIN A_MAKT ON ANEK_MATNR=MAKT_MATNR
--Add name of the user
LEFT JOIN A_V_USERNAME ON A_V_USERNAME.V_USERNAME_BNAME=ANEK_USNAM
--Add currency from T001
LEFT JOIN A_T001 ON ANEP_BUKRS=T001_BUKRS --AND (T001_SPRAS='E' OR T001_SPRAS='EN')
----Add TCURX for T001_WAERS 
LEFT JOIN B00_TCURX 
	ON T001_WAERS=TCURX_CURRKEY
-- Add currency factor from company currency to USD

LEFT JOIN B00_IT_TCURF TCURF_CUC
ON A_T001.T001_WAERS = TCURF_CUC.TCURF_FCURR
AND TCURF_CUC.TCURF_TCURR  = 'USD'  
AND TCURF_CUC.TCURF_GDATU = (
	SELECT TOP 1 B00_IT_TCURF.TCURF_GDATU
	FROM B00_IT_TCURF
	WHERE A_T001.T001_WAERS = B00_IT_TCURF.TCURF_FCURR AND 
			B00_IT_TCURF.TCURF_TCURR  = 'USD'  AND
			B00_IT_TCURF.TCURF_GDATU <= ANEP_BZDAT
	ORDER BY B00_IT_TCURF.TCURF_GDATU DESC
	)
-- Add exchange rate from company currency to USD
LEFT JOIN B00_IT_TCURR TCURR_CUC
	ON A_T001.T001_WAERS = TCURR_CUC.TCURR_FCURR
	AND TCURR_CUC.TCURR_TCURR  = 'USD'  
	AND TCURR_CUC.TCURR_GDATU = (
		SELECT TOP 1 B00_IT_TCURR.TCURR_GDATU
		FROM B00_IT_TCURR
		WHERE A_T001.T001_WAERS = B00_IT_TCURR.TCURR_FCURR AND 
				B00_IT_TCURR.TCURR_TCURR  = 'USD'  AND
				B00_IT_TCURR.TCURR_GDATU <= ANEP_BZDAT
		ORDER BY B00_IT_TCURR.TCURR_GDATU DESC
		) 

--Step 2 Rename the fields
EXEC SP_RENAME_FIELD 'B27_' ,'B27_01_IT_ASSET_POSTING'

END
GO
