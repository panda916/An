USE [DIVA_MASTER_SCRIPT]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

CREATE   PROCEDURE [dbo].[script_DC09_OTC_COPA]
WITH EXECUTE AS CALLER
AS
--DYNAMIC_SCRIPT_START
/* Initiate the log */ 
----Create database log table if it does not exist
IF OBJECT_ID('LOG_SP_EXECUTION', 'U') IS NULL BEGIN CREATE TABLE [DBO].[LOG_SP_EXECUTION] ([DATABASE] NVARCHAR(MAX) NULL,[OBJECT] NVARCHAR(MAX) NULL,[OBJECT_TYPE] NVARCHAR(MAX) NULL,[USER] NVARCHAR(MAX) NULL,[DATE] DATE NULL,[TIME] TIME NULL,[DESCRIPTION] NVARCHAR(MAX) NULL,[TABLE] NVARCHAR(MAX),[ROWS] INT) END
 
--Log start of procedure
INSERT INTO [DBO].[LOG_SP_EXECUTION] ([DATABASE],[OBJECT],[OBJECT_TYPE],[USER],[DATE],[TIME],[DESCRIPTION],[TABLE],[ROWS])
SELECT DB_NAME(),OBJECT_NAME(@@PROCID),'P',SYSTEM_USER,CONVERT(DATE,GETDATE()),CONVERT(TIME,GETDATE()),'Procedure started',NULL,NULL
 
/* Initialize parameters from globals table */
 
     DECLARE  
                      @CURRENCY NVARCHAR(MAX)                 = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'currency')
                     ,@DATE1 NVARCHAR(MAX)                           = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'date1')
                     ,@DATE2 NVARCHAR(MAX)                           = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'date2')
                     ,@DOWNLOADDATE NVARCHAR(MAX)             = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'downloaddate')
                     ,@DATEFORMAT VARCHAR(3)             = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'dateformat')
                     ,@EXCHANGERATETYPE NVARCHAR(MAX)  = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'exchangeratetype')
                     ,@LANGUAGE1 NVARCHAR(MAX)                = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'language1')
                     ,@LANGUAGE2 NVARCHAR(MAX)                = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'language2')
                     ,@YEAR NVARCHAR(MAX)                     = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'year')
                     ,@ID NVARCHAR(MAX)                       = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'id')
                     ,@LIMIT_RECORDS INT                    = CAST((SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'LIMIT_RECORDS') AS INT)
					 ,@ZV_SAME_QUARTER_BY_BLDAT NVARCHAR(MAX) = ISNULL((SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'ZV_SAME_QUARTER_BY_BLDAT'), '')


--Step 1/ Compare values (USD) between table B18_11_IT_COPA_BILLING_NEW (use TCURR and TCURF) and table B18_11_IT_COPA_BILLING (use AM_EXCHNG)
EXEC SP_DROPTABLE 	'DC09_01_IT_COPA_BILLING' 
SELECT 
	A.B18_11_VBRK_VBELN,
	A.B18_11_VBRK_BUKRS, 
	A.B18_11_VBRK_WAERK,
	A_T001.T001_WAERS,
	MAX(B18_11_VBRK_FKDAT) AS B18_11_VBRK_FKDAT, 
	SUM(A.B18_11_ZF_VBRP_NETWR_S) AS B18_11_ZF_VBRP_NETWR_S,
	SUM(B.B18_11_ZF_VBRP_NETWR_S_CUC) AS B18_11_ZF_VBRP_NETWR_S_CUC_OLD,
	SUM(A.B18_11_ZF_VBRP_NETWR_S_CUC) AS B18_11_ZF_VBRP_NETWR_S_CUC_NEW,
	MAX(COALESCE(EXCHNG_RATIO,1)) AS EXCH_RATE_OLD,
	MAX(C.VBRP_KURSK * COALESCE(TCURF_COC.TCURF_TFACT,1)/COALESCE(TCURF_COC.TCURF_FFACT,1) * COALESCE(CAST(TCURR_CUC.TCURR_UKURS AS FLOAT),1) * COALESCE(TCURF_CUC.TCURF_TFACT,1) / COALESCE(TCURF_CUC.TCURF_FFACT,1)) AS EXCH_RATE_NEW
INTO DC09_01_IT_COPA_BILLING
FROM (
		SELECT DISTINCT 
			B18_11_VBRK_VBELN,
			B18_11_VBRK_BUKRS,
			MAX(B18_11_VBRK_WAERK) AS B18_11_VBRK_WAERK,
			MAX(B18_11_VBRK_FKDAT) AS B18_11_VBRK_FKDAT,
			SUM(B18_11_ZF_VBRP_NETWR_S) AS B18_11_ZF_VBRP_NETWR_S,
			SUM(B18_11_ZF_VBRP_NETWR_S_CUC) AS B18_11_ZF_VBRP_NETWR_S_CUC
		FROM B18_11_IT_COPA_BILLING_NEW
		GROUP BY 
			B18_11_VBRK_VBELN, 
			B18_11_VBRK_BUKRS
	) A

INNER JOIN (
		SELECT DISTINCT 
			B18_11_VBRK_VBELN,
			B18_11_VBRK_BUKRS,
			SUM(B18_11_ZF_VBRP_NETWR_S_CUC) AS B18_11_ZF_VBRP_NETWR_S_CUC
		FROM B18_11_IT_COPA_BILLING
		GROUP BY 
			B18_11_VBRK_VBELN, 
			B18_11_VBRK_BUKRS
	) B
	ON A.B18_11_VBRK_VBELN = B.B18_11_VBRK_VBELN
	AND A.B18_11_VBRK_BUKRS = B.B18_11_VBRK_BUKRS
INNER JOIN (SELECT DISTINCT VBRP_VBELN, VBRP_KURSK FROM A_VBRP)  AS C
	ON A.B18_11_VBRK_VBELN = C.VBRP_VBELN
LEFT JOIN A_T001
	ON  A.B18_11_VBRK_BUKRS = A_T001.T001_BUKRS
	
LEFT JOIN AM_EXCHNG
	ON  AM_EXCHNG.EXCHNG_FROM = A_T001.T001_WAERS
	AND AM_EXCHNG.EXCHNG_TO   =  @currency

LEFT JOIN B00_IT_TCURF TCURF_CUC
	ON A_T001.T001_WAERS = TCURF_CUC.TCURF_FCURR
	AND TCURF_CUC.TCURF_TCURR  = @currency  
	AND TCURF_CUC.TCURF_GDATU = (
	SELECT TOP 1 B00_IT_TCURF.TCURF_GDATU
	FROM B00_IT_TCURF
	WHERE A_T001.T001_WAERS = B00_IT_TCURF.TCURF_FCURR AND 
			B00_IT_TCURF.TCURF_TCURR  = @currency  AND
			B00_IT_TCURF.TCURF_GDATU <= A.B18_11_VBRK_FKDAT
	ORDER BY B00_IT_TCURF.TCURF_GDATU DESC
	)

LEFT JOIN B00_IT_TCURR TCURR_CUC
	ON A_T001.T001_WAERS = TCURR_CUC.TCURR_FCURR
	AND TCURR_CUC.TCURR_TCURR  = @currency  
	AND TCURR_CUC.TCURR_GDATU = (
		SELECT TOP 1 B00_IT_TCURR.TCURR_GDATU
		FROM B00_IT_TCURR
		WHERE A_T001.T001_WAERS = B00_IT_TCURR.TCURR_FCURR AND 
				B00_IT_TCURR.TCURR_TCURR  = @currency  AND
				B00_IT_TCURR.TCURR_GDATU <= A.B18_11_VBRK_FKDAT
		ORDER BY B00_IT_TCURR.TCURR_GDATU DESC
		) 

LEFT JOIN B00_IT_TCURF TCURF_COC
ON A.B18_11_VBRK_WAERK = TCURF_COC.TCURF_FCURR
AND TCURF_COC.TCURF_TCURR  = T001_WAERS  
AND TCURF_COC.TCURF_GDATU = (
	SELECT TOP 1 B00_IT_TCURF.TCURF_GDATU
	FROM B00_IT_TCURF
	WHERE A.B18_11_VBRK_WAERK = B00_IT_TCURF.TCURF_FCURR AND 
			B00_IT_TCURF.TCURF_TCURR  = T001_WAERS  AND
			B00_IT_TCURF.TCURF_GDATU <= A.B18_11_VBRK_FKDAT
	ORDER BY B00_IT_TCURF.TCURF_GDATU DESC
	)	 

GROUP BY A.B18_11_VBRK_VBELN,
	A.B18_11_VBRK_BUKRS, 
	A.B18_11_VBRK_WAERK,
	A_T001.T001_WAERS


EXEC SP_UNNAME_FIELD 'B18_11_',DC09_01_IT_COPA_BILLING
EXEC SP_RENAME_FIELD 'DC09_01_',DC09_01_IT_COPA_BILLING

GO
