USE [DIVA_MASTER_SCRIPT]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

CREATE   PROCEDURE [dbo].[script_DC12_MATERIAL_MOVEMENT_CUBE]
WITH EXECUTE AS CALLER
AS
--DYNAMIC_SCRIPT_START
/* Initiate the log */ 
----Create database log table if it does not exist
IF OBJECT_ID('LOG_SP_EXECUTION', 'U') IS NULL BEGIN CREATE TABLE [DBO].[LOG_SP_EXECUTION] ([DATABASE] NVARCHAR(MAX) NULL,[OBJECT] NVARCHAR(MAX) NULL,[OBJECT_TYPE] NVARCHAR(MAX) NULL,[USER] NVARCHAR(MAX) NULL,[DATE] DATE NULL,[TIME] TIME NULL,[DESCRIPTION] NVARCHAR(MAX) NULL,[TABLE] NVARCHAR(MAX),[ROWS] INT) END
 
--Log start of procedure
INSERT INTO [DBO].[LOG_SP_EXECUTION] ([DATABASE],[OBJECT],[OBJECT_TYPE],[USER],[DATE],[TIME],[DESCRIPTION],[TABLE],[ROWS])
SELECT DB_NAME(),OBJECT_NAME(@@PROCID),'P',SYSTEM_USER,CONVERT(DATE,GETDATE()),CONVERT(TIME,GETDATE()),'Procedure started',NULL,NULL
 

/* Initialize parameters from globals table */
 
     DECLARE  
                      @CURRENCY NVARCHAR(MAX)                 = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'currency')
                     ,@DATE1 NVARCHAR(MAX)                           = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'date1')
                     ,@DATE2 NVARCHAR(MAX)                           = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'date2')
                     ,@DOWNLOADDATE NVARCHAR(MAX)             = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'downloaddate')
                     ,@DATEFORMAT VARCHAR(3)             = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'dateformat')
                     ,@EXCHANGERATETYPE NVARCHAR(MAX)  = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'exchangeratetype')
                     ,@LANGUAGE1 NVARCHAR(MAX)                = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'language1')
                     ,@LANGUAGE2 NVARCHAR(MAX)                = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'language2')
                     ,@YEAR NVARCHAR(MAX)                     = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'year')
                     ,@ID NVARCHAR(MAX)                       = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'id')
                     ,@LIMIT_RECORDS INT                    = CAST((SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'LIMIT_RECORDS') AS INT)
					 ,@ZV_SAME_QUARTER_BY_BLDAT NVARCHAR(MAX) = ISNULL((SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'ZV_SAME_QUARTER_BY_BLDAT'), '')


--Step 1/ Compare values (USD) between table B24_01_IT_MATERIAL_MOVEMENT_NEW (use TCURR and TCURF) and table B24_01_IT_MATERIAL_MOVEMENT (use AM_EXCHNG)
EXEC SP_DROPTABLE 	'DC12_01_IT_MATERIAL_MOVEMENT' 
SELECT 
	A.B24_01_MSEG_BUKRS,
	A.B24_01_MSEG_MJAHR,
	A.B24_01_MSEG_MBLNR,
	A.B24_01_MSEG_ZEILE,
	A_T001.T001_WAERS,
	B.B24_01_ZF_MSEG_DMBTR_SIGNED AS B24_01_ZF_MSEG_DMBTR_SIGNED,
	B.B24_01_ZF_MSEG_DMBTR_SIGNED_CUC AS B24_01_ZF_MSEG_DMBTR_SIGNED_CUC_OLD,
	A.B24_01_ZF_MSEG_DMBTR_SIGNED_CUC AS B24_01_ZF_MSEG_DMBTR_SIGNED_CUC_NEW,
    ISNULL(EXCHNG_RATIO,1) AS EXCH_RATE_CUC_OLD,
	COALESCE(CAST(TCURR_CUC.TCURR_UKURS AS FLOAT),1) * COALESCE(TCURF_CUC.TCURF_TFACT,1) / COALESCE(TCURF_CUC.TCURF_FFACT,1)*COALESCE(TCURX_FACTOR,1)*COALESCE(TCURX_FACTOR,1) AS EXCH_RATE_CUC_NEW
INTO DC12_01_IT_MATERIAL_MOVEMENT
FROM (SELECT DISTINCT B24_01_MSEG_BUKRS,
					  B24_01_MSEG_MJAHR,
					  B24_01_MSEG_MBLNR,
					  B24_01_MSEG_ZEILE,
					  MAX(B24_01_MSEG_WAERS) AS B24_01_MSEG_WAERS,
					  MAX(B24_01_MKPF_BUDAT) AS B24_01_MKPF_BUDAT,
					  SUM(B24_01_ZF_MSEG_DMBTR_SIGNED_CUC) AS B24_01_ZF_MSEG_DMBTR_SIGNED_CUC
      FROM B24_01_IT_MATERIAL_MOVEMENT_NEW 
	  WHERE B24_01_MSEG_SHKZG = 'S'
	  GROUP BY  B24_01_MSEG_BUKRS,
				B24_01_MSEG_MJAHR,
				B24_01_MSEG_MBLNR,
				B24_01_MSEG_ZEILE ) A

INNER JOIN (SELECT DISTINCT B24_01_MSEG_BUKRS,
					  B24_01_MSEG_MJAHR,
					  B24_01_MSEG_MBLNR,
					  B24_01_MSEG_ZEILE,
					  SUM(B24_01_ZF_MSEG_DMBTR_SIGNED) AS B24_01_ZF_MSEG_DMBTR_SIGNED,
	                  SUM(B24_01_ZF_MSEG_DMBTR_SIGNED_CUC) AS B24_01_ZF_MSEG_DMBTR_SIGNED_CUC
            FROM B24_01_IT_MATERIAL_MOVEMENT
			WHERE B24_01_MSEG_SHKZG = 'S'
			GROUP BY  B24_01_MSEG_BUKRS,
					  B24_01_MSEG_MJAHR,
					  B24_01_MSEG_MBLNR,
					  B24_01_MSEG_ZEILE ) B
	ON A.B24_01_MSEG_BUKRS = B.B24_01_MSEG_BUKRS
	AND A.B24_01_MSEG_MJAHR = B.B24_01_MSEG_MJAHR
	AND A.B24_01_MSEG_MBLNR = B.B24_01_MSEG_MBLNR
	AND A.B24_01_MSEG_ZEILE = B.B24_01_MSEG_ZEILE

LEFT JOIN A_T001
	ON  A.B24_01_MSEG_BUKRS = A_T001.T001_BUKRS	  
	
-- Add the exchange rate
LEFT JOIN AM_EXCHNG 
	ON A.B24_01_MSEG_WAERS =EXCHNG_FROM 
	AND EXCHNG_TO= @currency 

LEFT JOIN B00_TCURX 
	ON T001_WAERS=TCURX_CURRKEY

-- Add currency factor from company currency to USD

LEFT JOIN B00_IT_TCURF TCURF_CUC
ON A_T001.T001_WAERS = TCURF_CUC.TCURF_FCURR
AND TCURF_CUC.TCURF_TCURR  = @currency  
AND TCURF_CUC.TCURF_GDATU = (
	SELECT TOP 1 B00_IT_TCURF.TCURF_GDATU
	FROM B00_IT_TCURF
	WHERE A_T001.T001_WAERS = B00_IT_TCURF.TCURF_FCURR AND 
			B00_IT_TCURF.TCURF_TCURR  = @currency  AND
			B00_IT_TCURF.TCURF_GDATU <= A.B24_01_MKPF_BUDAT
	ORDER BY B00_IT_TCURF.TCURF_GDATU DESC
	)
-- Add exchange rate from company currency to USD
LEFT JOIN B00_IT_TCURR TCURR_CUC
	ON A_T001.T001_WAERS = TCURR_CUC.TCURR_FCURR
	AND TCURR_CUC.TCURR_TCURR  = @currency  
	AND TCURR_CUC.TCURR_GDATU = (
		SELECT TOP 1 B00_IT_TCURR.TCURR_GDATU
		FROM B00_IT_TCURR
		WHERE A_T001.T001_WAERS = B00_IT_TCURR.TCURR_FCURR AND 
				B00_IT_TCURR.TCURR_TCURR  = @currency  AND
				B00_IT_TCURR.TCURR_GDATU <= A.B24_01_MKPF_BUDAT
		ORDER BY B00_IT_TCURR.TCURR_GDATU DESC
		) 


EXEC SP_UNNAME_FIELD 'B24_01_',DC12_01_IT_MATERIAL_MOVEMENT
EXEC SP_RENAME_FIELD 'DC12_01_',DC12_01_IT_MATERIAL_MOVEMENT
GO
