USE [DIVA_MASTER_SCRIPT_S4HANA]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO



CREATE      PROCEDURE [dbo].[script_B02_CBE_BP_BANK_OPTIMIZED]
WITH EXECUTE AS CALLER
AS
--DYNAMIC_SCRIPT_START

BEGIN
/*
	Step 1: Create a cube for BP bank and bank info from BUT0BK and BNKA
*/     
EXEC SP_REMOVE_TABLES 'B02_01_TT_CBE_BP_BANK'      --'CBE_Business Partner Bank Temp 1'
SELECT 
	A_BUT0BK.BUT0BK_PARTNER AS BUT0BK_PARTNER --Business Partner
	,A_BUT0BK.BUT0BK_BKVID AS BUT0BK_BKVID --Bank details
	,A_BUT0BK.BUT0BK_BANKS AS BUT0BK_BANKS --Bank Country
	,A_BUT0BK.BUT0BK_BANKL AS BUT0BK_BANKL --Bank Key
	,A_BNKA.BNKA_BANKA AS BNKA_BANKA --Name of bank
	,A_BNKA.BNKA_BRNCH AS BNKA_BRNCH --Bank Branch
	,A_BUT0BK.BUT0BK_BANKN AS BUT0BK_BANKN --Bank acct
	,A_BUT0BK.BUT0BK_BKONT AS BUT0BK_BKONT --Control Key
	,A_BUT0BK.BUT0BK_BKREF AS BUT0BK_BKREF --Reference
	,A_BUT0BK.BUT0BK_KOINH AS BUT0BK_KOINH --Account Holder
	,A_BUT0BK.BUT0BK_BKEXT AS BUT0BK_BKEXT --Extern.bank ID
	,A_BUT0BK.BUT0BK_XEZER AS BUT0BK_XEZER --Collect.author.
	,A_BUT0BK.BUT0BK_ACCNAME AS BUT0BK_ACCNAME --Account Name
	,A_BUT0BK.BUT0BK_MOVE_BKVID AS BUT0BK_MOVE_BKVID --Target Details
	,A_BUT0BK.BUT0BK_BK_VALID_FROM AS BUT0BK_BK_VALID_FROM --Valid From
	,A_BUT0BK.BUT0BK_BK_VALID_TO AS BUT0BK_BK_VALID_TO --Valid To
	,A_BUT0BK.BUT0BK_BK_MOVE_DATE AS BUT0BK_BK_MOVE_DATE --Date of Change
	,A_BUT0BK.BUT0BK_IBAN AS BUT0BK_IBAN --IBAN
	,A_BUT0BK.BUT0BK_BANK_GUID AS BUT0BK_BANK_GUID --Bank GUID
	,A_BUT0BK.BUT0BK_ACCOUNT_ID AS BUT0BK_ACCOUNT_ID --Bank Acct. No.
	,A_BUT0BK.BUT0BK_CHECK_DIGIT AS BUT0BK_CHECK_DIGIT --Bnk Acct Check Digit
	,A_BUT0BK.BUT0BK_ACCOUNT_TYPE AS BUT0BK_ACCOUNT_TYPE --Bank Acct Type
	,CASE 
		WHEN A_BUT0BK.BUT0BK_BANKL = '' AND A_BUT0BK.BUT0BK_BANKN = '' THEN NULL
		ELSE CONCAT(A_BUT0BK.BUT0BK_BANKL,'_',A_BUT0BK.BUT0BK_BANKN)
	END  ZF_BUT0BK_BANK_ACC --Z_Bank Account
INTO B02_01_TT_CBE_BP_BANK --CBE_Business Partner Bank Temp 1
FROM
	dbo.A_BUT0BK

LEFT JOIN dbo.A_BNKA ON 
		A_BUT0BK.BUT0BK_BANKL=A_BNKA.BNKA_BANKL
	AND A_BUT0BK.BUT0BK_BANKS=A_BNKA.BNKA_BANKS

WHERE
	A_BUT0BK.BUT0BK_BKVID <> '' AND A_BUT0BK.BUT0BK_BKVID IS NOT NULL


/*
	Step 2: List all Bank account belong to more than one BP nr
*/
EXEC SP_REMOVE_TABLES 'B02_02_TT_CBE_BP_BANK_DUP_FLAG'
SELECT BUT0BK_BANKN --Bank acct
INTO B02_02_TT_CBE_BP_BANK_DUP_FLAG --CBE_Business Partner Bank Dupplicate Flag
FROM B02_01_TT_CBE_BP_BANK --CBE_Business Partner Bank Temp 1
WHERE BUT0BK_BANKN  <> '' /*Bank acct*/
GROUP BY BUT0BK_BANKN --Bank acct
HAVING COUNT(DISTINCT BUT0BK_PARTNER) > 1

/*
	Step 3: Put all bank account with dupplicate flag to main cube
*/
EXEC SP_REMOVE_TABLES 'B02_03_IT_CBE_BP_BANK' --'CBE_Business Partner Bank'
SELECT 
	B02_01_TT_CBE_BP_BANK.*, /*CBE_Business Partner Bank Temp 1*/
	IIF(B02_02_TT_CBE_BP_BANK_DUP_FLAG.BUT0BK_BANKN  IS NOT NULL, 'X', '') ZF_BUT0BK_BANKN_BANK_ACC_DUP_FLAG --Z_Bank Account Duplicate Flag  
INTO B02_03_IT_CBE_BP_BANK --CBE_Business Partner Bank
FROM B02_01_TT_CBE_BP_BANK --CBE_Business Partner Bank Temp 1

-- Get Bank account duplicate flag
LEFT JOIN B02_02_TT_CBE_BP_BANK_DUP_FLAG --CBE_Business Partner Bank Dupplicate Flag
ON B02_02_TT_CBE_BP_BANK_DUP_FLAG.BUT0BK_BANKN  = B02_01_TT_CBE_BP_BANK.BUT0BK_BANKN --CBE_Business Partner Bank Temp 1.Bank acct

/*
	Delete temporary tables
*/
	EXEC SP_REMOVE_TABLES '%_TT_%'

-- Rename field in table

EXEC SP_RENAME_FIELD 'B02_', 'B02_03_IT_CBE_BP_BANK'


END

GO
