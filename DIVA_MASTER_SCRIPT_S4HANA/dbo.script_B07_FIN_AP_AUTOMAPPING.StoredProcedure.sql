USE [DIVA_MASTER_SCRIPT_S4HANA]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO


CREATE        PROC [dbo].[script_B07_FIN_AP_AUTOMAPPING]
WITH EXECUTE AS CALLER
AS
--DYNAMIC_SCRIPT_START
--USE DIVA_SIE_US_FY22


/* Initiate the log */  
--Create database log table if it does not exist
IF OBJECT_ID('LOG_SP_EXECUTION', 'U') IS NULL BEGIN CREATE TABLE [DBO].[LOG_SP_EXECUTION] ([DATABASE] NVARCHAR(MAX) NULL,[OBJECT] NVARCHAR(MAX) NULL,[OBJECT_TYPE] NVARCHAR(MAX) NULL,[USER] NVARCHAR(MAX) NULL,[DATE] DATE NULL,[TIME] TIME NULL,[DESCRIPTION] NVARCHAR(MAX) NULL,[TABLE] NVARCHAR(MAX),[ROWS] INT) END

--Log start of procedure
INSERT INTO [DBO].[LOG_SP_EXECUTION] ([DATABASE],[OBJECT],[OBJECT_TYPE],[USER],[DATE],[TIME],[DESCRIPTION],[TABLE],[ROWS])
SELECT DB_NAME(),OBJECT_NAME(@@PROCID),'P',SYSTEM_USER,CONVERT(DATE,GETDATE()),CONVERT(TIME,GETDATE()),'Procedure started',NULL,NULL

	/* Initialize parameters from globals table */
    DECLARE  
				@CURRENCY NVARCHAR(3)                 = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'currency')
				,@DATE1 NVARCHAR(MAX)                           = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'date1')
				,@DATE2 NVARCHAR(MAX)                           = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'date2')
				,@DOWNLOADDATE NVARCHAR(MAX)             = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'downloaddate')
				,@DATEFORMAT VARCHAR(3)             = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'dateformat')
				,@EXCHANGERATETYPE NVARCHAR(MAX)  = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'exchangeratetype')
				,@LANGUAGE1 NVARCHAR(3)                = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'language1')
				,@LANGUAGE2 NVARCHAR(3)                = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'language2')
				,@LIMIT_RECORDS INT                    = CAST((SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'LIMIT_RECORDS') AS INT)
				,@FISCAL_YEAR_FROM NVARCHAR(MAX)					= (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'FISCAL_YEAR_FROM')
				,@FISCAL_YEAR_TO NVARCHAR(MAX)					= (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'FISCAL_YEAR_TO')
                 SET DATEFORMAT @DATEFORMAT;


  /*Change history comments*/

  /*  
    --------------------------------------------------------------
    Update history
    --------------------------------------------------------------
    Date          | Who     | Description
    DD-MM-YYYY        Initials    Initial version
    27-11-2020        Vinh Le     Create first version of AP mapping automatically
	15-08-2024		  Nam		   Remove fields from BSEG and remove XANET filters
  */
	  /*Test mode*/

	  SET ROWCOUNT @LIMIT_RECORDS


	  
 
  --STep 0.1/ Create extra flag for AR,AP clearing fisrt account from accouting scheme
  -- For both debit and credit
	  EXEC SP_DROPTABLE B07_00_TT_AP_ADD_EXTRA_FLAG

	  SELECT *,
	  LEFT(ZF_DEBIT_ACCT_IS_AP_CLEARING,1) ZF_DEBIT_FIRST_ACCOUNT_AP_CLEARING,
	  LEFT(ZF_CREDIT_ACCT_IS_AP_CLEARING,1) ZF_CREDIT_FIRST_ACCOUNT_AP_CLEARING,
	  LEFT(ZF_DEBIT_ACCT_IS_AR_CLEARING,1) ZF_DEBIT_FIRST_ACCOUNT_AR_CLEARING,
	  LEFT(ZF_CREDIT_ACCT_IS_AR_CLEARING,1) ZF_CREDIT_FIRST_ACCOUNT_AR_CLEARING,
	  '' ZF_DEBIT_FIRST_ACCOUNT_IS_BANK_OR_CLEAR,
	  '' ZF_CREDIT_FIRST_ACCOUNT_IS_BANK_OR_CLEAR,
      SUBSTRING(ZF_CREDIT_ACCOUNTS,1, IIF(CHARINDEX('_',ZF_CREDIT_ACCOUNTS)=0,LEN(ZF_CREDIT_ACCOUNTS),CHARINDEX('_',ZF_CREDIT_ACCOUNTS)-1)) AS ZF_FIRST_CREDIT_ACCOUNT,
	  SUBSTRING(ZF_DEBIT_ACCOUNTS,1, IIF(CHARINDEX('_',ZF_DEBIT_ACCOUNTS)=0,LEN(ZF_DEBIT_ACCOUNTS),CHARINDEX('_',ZF_DEBIT_ACCOUNTS)-1)) AS ZF_FIRST_DEBIT_ACCOUNT
	  INTO B07_00_TT_AP_ADD_EXTRA_FLAG
	  FROM B04_11_IT_AP_ACC_SCH 


--Update the flag where account relate to bank or AP clearing
	UPDATE B07_00_TT_AP_ADD_EXTRA_FLAG SET ZF_DEBIT_FIRST_ACCOUNT_IS_BANK_OR_CLEAR='Y' 
	WHERE ZF_DEBIT_FIRST_ACCOUNT_AP_CLEARING='Y' OR  LEFT(ZF_DEBIT_T012K_FEBKO_SKB1_HBKID_XGKON_FLAG_LIST, 1)='Y'
	UPDATE B07_00_TT_AP_ADD_EXTRA_FLAG SET ZF_CREDIT_FIRST_ACCOUNT_IS_BANK_OR_CLEAR='Y' 
	WHERE ZF_CREDIT_FIRST_ACCOUNT_AP_CLEARING='Y' OR  LEFT(ZF_CREDIT_T012K_FEBKO_SKB1_HBKID_XGKON_FLAG_LIST, 1)='Y'

	/*
		  Step 1: Create a summary table before we flag the AP document with categories to we can check the mapping easier because it have less record than full AP documents tables.
	*/
	  EXEC SP_REMOVE_TABLES 'B07_01_TT_FIN_AP_INV_PAY_FLAGS'
	  SELECT DISTINCT
		  B07_00_TT_AP_ADD_EXTRA_FLAG.BKPF_TCODE,
		  B07_00_TT_AP_ADD_EXTRA_FLAG.ACDOCA_AWTYP,
		  B07_00_TT_AP_ADD_EXTRA_FLAG.LFA1_KTOKK,
		  B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_DEBIT_ACCOUNT_TYPES,
		  B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_CREDIT_ACCOUNT_TYPES,
		  B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_DEBIT_T012K_FEBKO_SKB1_HBKID_XGKON_FLAG_LIST,
		  B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_CREDIT_T012K_FEBKO_SKB1_HBKID_XGKON_FLAG_LIST,
		  B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_DEBIT_SKA1_GVTYP_FLAG_LIST,
		  B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_CREDIT_SKA1_GVTYP_FLAG_LIST,
		  B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_DOC_CLEARING_FLAG,
		  B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_REGUH_PAYMENT_FLAG,
		  B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_REGUP_INVOICE_FLAG,
		  B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_LIFNR_KUNNR_IS_SAME_FLAG,  
		  B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_DEBIT_FIRST_ACCOUNT_AP_CLEARING,
		  B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_CREDIT_FIRST_ACCOUNT_AP_CLEARING,
		  B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_DEBIT_FIRST_ACCOUNT_IS_BANK_OR_CLEAR,
		  B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_CREDIT_FIRST_ACCOUNT_IS_BANK_OR_CLEAR,
		  B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_DEBIT_FIRST_ACCOUNT_AR_CLEARING,
		  B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_CREDIT_FIRST_ACCOUNT_AR_CLEARING,
          ZF_FIRST_CREDIT_ACCOUNT,
	      ZF_FIRST_DEBIT_ACCOUNT,
		  'Y' AS UNCLASSIFIED_FLAG
	  INTO B07_01_TT_FIN_AP_INV_PAY_FLAGS
	  FROM B07_00_TT_AP_ADD_EXTRA_FLAG


	  /*
		Step 2: Add AP document type columns with default value is "Other". It will be update later
	  */

	  ALTER TABLE B07_01_TT_FIN_AP_INV_PAY_FLAGS ADD ZF_FLAG_SUMMARY NVARCHAR(100) DEFAULT 'Others' WITH VALUES;
	  ALTER TABLE B07_01_TT_FIN_AP_INV_PAY_FLAGS ADD ZF_SUPP_INV NVARCHAR(1) DEFAULT '' WITH VALUES;
	  ALTER TABLE B07_01_TT_FIN_AP_INV_PAY_FLAGS ADD ZF_SUPP_INV_CANC NVARCHAR(1) DEFAULT '' WITH VALUES;
	  ALTER TABLE B07_01_TT_FIN_AP_INV_PAY_FLAGS ADD ZF_SUPP_PAY NVARCHAR(1) DEFAULT '' WITH VALUES;
	  ALTER TABLE B07_01_TT_FIN_AP_INV_PAY_FLAGS ADD ZF_SUPP_PAY_CANC NVARCHAR(1) DEFAULT '' WITH VALUES;
	  ALTER TABLE B07_01_TT_FIN_AP_INV_PAY_FLAGS ADD ZF_OTHERS NVARCHAR(1) DEFAULT '' WITH VALUES;

-- UPdate other rules
-- FLag as Other where first Credit or Debit account like "A99%"
	      UPDATE B07_01_TT_FIN_AP_INV_PAY_FLAGS
	  		  SET ZF_FLAG_SUMMARY = 'Others', ZF_OTHERS = 'X', 
			      UNCLASSIFIED_FLAG = ''
		  WHERE UNCLASSIFIED_FLAG = 'Y' AND 
		  (
			 ZF_FIRST_CREDIT_ACCOUNT IN ( SELECT SKAT_SAKNR FROM AM_OTHER_ACCOUTS)
			 OR
			 ZF_FIRST_DEBIT_ACCOUNT IN ( SELECT SKAT_SAKNR FROM AM_OTHER_ACCOUTS)
		  )
	  
			/*
				Step 3.3: Flag all documents have
							Debit on (SKA1_GVTYP_BSEG <> "" OR  BSEG_KOART = S) AND NOT(ZF_BSEG_HKONT_PAYFLAG) 
						 		Khoi:		AND Not found in AP clearing and  Not found in AR clearing
							Credit on account type 'K'
							BELNR <> AUGBL
							AWTYP = 'RMRP'
						  to Supplier invoice
			*/
			UPDATE B07_01_TT_FIN_AP_INV_PAY_FLAGS
			SET ZF_FLAG_SUMMARY = 'Supplier invoice', ZF_SUPP_INV = 'X', 
			    UNCLASSIFIED_FLAG = ''
			WHERE UNCLASSIFIED_FLAG = 'Y' AND
				  
				  (
					LEFT(ZF_DEBIT_SKA1_GVTYP_FLAG_LIST,1) <> '_' OR
					LEFT(ZF_DEBIT_ACCOUNT_TYPES, 1) = 'S'
				  )
					 --KHOI	
				   AND ZF_DEBIT_FIRST_ACCOUNT_IS_BANK_OR_CLEAR<>'Y'
				   AND ZF_DEBIT_FIRST_ACCOUNT_AR_CLEARING<>'Y'
				  AND LEFT(ZF_CREDIT_ACCOUNT_TYPES, 1) = 'K'
				  AND ACDOCA_AWTYP = 'RMRP'

			/*
				Step 3.4: Flag all documents have
							Debit on account type 'K'
							Credit on (SKA1_GVTYP_BSEG<> "" OR  BSEG_KOART = S) AND NOT(ZF_BSEG_HKONT_PAYFLAG) 
							Khoi: Not found in AP clearing
							AUGBL <> BELNR
							AWTYP= RMRP
						  to Supplier invoice cancellation
			*/
			UPDATE B07_01_TT_FIN_AP_INV_PAY_FLAGS
			SET ZF_FLAG_SUMMARY = 'Supplier invoice cancellation', ZF_SUPP_INV_CANC = 'X', 
			    UNCLASSIFIED_FLAG = ''
			WHERE UNCLASSIFIED_FLAG = 'Y' AND
			LEFT(ZF_DEBIT_ACCOUNT_TYPES, 1) = 'K' AND
			(
				LEFT(ZF_CREDIT_SKA1_GVTYP_FLAG_LIST, 1) <> '_' OR
				LEFT(ZF_CREDIT_ACCOUNT_TYPES, 1) = 'S'
			)
			--Khoi
		    AND ZF_CREDIT_FIRST_ACCOUNT_IS_BANK_OR_CLEAR<>'Y'
			AND ACDOCA_AWTYP = 'RMRP' 

			/*
				Step 3.5: Flag all documents have
							Debit on BSEG_KOART = K
							Credit on ZF_BSEG_HKONT_PAYFLAG 
							Khoi: Or found in AP clearing
							BSAK_BELNR found in REGUH_VBLNR
							AWTYP <> 'RMRP'
						  to Supplier payment
			*/
			UPDATE B07_01_TT_FIN_AP_INV_PAY_FLAGS
			SET ZF_FLAG_SUMMARY = 'Supplier payment', ZF_SUPP_PAY = 'X', 
			    UNCLASSIFIED_FLAG = ''
			WHERE UNCLASSIFIED_FLAG = 'Y' AND
				  LEFT(ZF_DEBIT_ACCOUNT_TYPES, 1) = 'K'
				  AND (
						 ZF_CREDIT_FIRST_ACCOUNT_IS_BANK_OR_CLEAR='Y' --Khoi
					  )
				  AND ZF_REGUH_PAYMENT_FLAG = 'X'
				  AND ACDOCA_AWTYP <> 'RMRP'



			/*
				Step 3.6: Flag all documents have
							Debit on ZF_BSEG_HKONT_PAYFLAG 
							Khoi: or found in AP clearing
							Credit on BSEG_KOART = K
							BSAK_BELNR found in REGUH_VBLNR
							AWTYP <> 'RMRP'
						  to Supplier payment cancellation
			*/
			UPDATE B07_01_TT_FIN_AP_INV_PAY_FLAGS
			SET ZF_FLAG_SUMMARY = 'Supplier payment cancellation', ZF_SUPP_PAY_CANC = 'X', 
			    UNCLASSIFIED_FLAG = ''
			WHERE UNCLASSIFIED_FLAG = 'Y' AND
				  (
					--Khoi
					ZF_DEBIT_FIRST_ACCOUNT_IS_BANK_OR_CLEAR='Y'
				  )
				  AND LEFT(ZF_CREDIT_ACCOUNT_TYPES, 1) = 'K'
				  AND ZF_REGUH_PAYMENT_FLAG = 'X'
				  AND ACDOCA_AWTYP <> 'RMRP'


			/*
				Step 3.7: Flag all documents have
							Debit on (SKA1_GVTYP_BSEG<> "" OR  BSEG_KOART = S) AND NOT(ZF_BSEG_HKONT_PAYFLAG) 
							Khoi: and Not found in AP clearing
							Credit on BSEG_KOART = K
							AUGBL <> BELNR
						  to Supplier invoice
			*/
			UPDATE B07_01_TT_FIN_AP_INV_PAY_FLAGS
			SET ZF_FLAG_SUMMARY = 'Supplier invoice', ZF_SUPP_INV = 'X', 
			    UNCLASSIFIED_FLAG = ''
			WHERE UNCLASSIFIED_FLAG = 'Y' AND
				  (
					LEFT(ZF_DEBIT_SKA1_GVTYP_FLAG_LIST,1) <> '_' OR
					LEFT(ZF_DEBIT_ACCOUNT_TYPES, 1) = 'S'
				  )
				  --Khoi
				  AND ZF_DEBIT_FIRST_ACCOUNT_IS_BANK_OR_CLEAR<>'Y'
				  AND ZF_DEBIT_FIRST_ACCOUNT_AR_CLEARING<>'Y'
				  AND LEFT(ZF_CREDIT_ACCOUNT_TYPES, 1) = 'K'


			/*
				Step 3.8: Flag all documents have
							Debit on account type 'K'
							Credit on (SKA1_GVTYP_BSEG<> "" OR  BSEG_KOART = S) AND NOT(ZF_BSEG_HKONT_PAYFLAG) 
							Khoi: Not found in AP clearing
							AUGBL <> BELNR
						  to Supplier invoice cancellation
			*/
			UPDATE B07_01_TT_FIN_AP_INV_PAY_FLAGS
			SET ZF_FLAG_SUMMARY = 'Supplier invoice cancellation', ZF_SUPP_INV_CANC = 'X', 
			    UNCLASSIFIED_FLAG = ''
			WHERE UNCLASSIFIED_FLAG = 'Y' AND
			LEFT(ZF_DEBIT_ACCOUNT_TYPES, 1) = 'K' AND
			(
				LEFT(ZF_CREDIT_SKA1_GVTYP_FLAG_LIST, 1) <> '_' OR
				LEFT(ZF_CREDIT_ACCOUNT_TYPES, 1) = 'S'
			)
		 --Khoi
		    AND ZF_CREDIT_FIRST_ACCOUNT_IS_BANK_OR_CLEAR<>'Y'

			/*
				Step 3.9: Flag all documents have
							Debit on BSEG_KOART = K
							Credit on ZF_BSEG_HKONT_PAYFLAG 
							Khoi: or found in AP clearing
							AWTYP <> 'RMRP'
						  to Supplier payment
			*/
			UPDATE B07_01_TT_FIN_AP_INV_PAY_FLAGS
			SET ZF_FLAG_SUMMARY = 'Supplier payment', ZF_SUPP_PAY = 'X', 
			    UNCLASSIFIED_FLAG = ''
			WHERE UNCLASSIFIED_FLAG = 'Y' AND
				  LEFT(ZF_DEBIT_ACCOUNT_TYPES, 1) = 'K'
				  AND (
					  --Khoi
						 ZF_CREDIT_FIRST_ACCOUNT_IS_BANK_OR_CLEAR='Y'
					  )
				  AND ACDOCA_AWTYP <> 'RMRP'

			
			/*
				Step 3.10: Flag all documents have
							Debit on ZF_BSEG_HKONT_PAYFLAG 
							Khoi: or found in AP clearing
							Credit on BSEG_KOART = K
							AWTYP <> 'RMRP'
						  to Supplier payment cancellation
			*/
			UPDATE B07_01_TT_FIN_AP_INV_PAY_FLAGS
			SET ZF_FLAG_SUMMARY = 'Supplier payment cancellation', ZF_SUPP_PAY_CANC = 'X', 
			    UNCLASSIFIED_FLAG = ''
			WHERE UNCLASSIFIED_FLAG = 'Y' AND
				  (
					  ZF_DEBIT_FIRST_ACCOUNT_IS_BANK_OR_CLEAR='Y' --Khoi
				  )
				  AND LEFT(ZF_CREDIT_ACCOUNT_TYPES, 1) = 'K'
				  AND ACDOCA_AWTYP <> 'RMRP'


			/*
				Step 3.11: Flag all documents have
							Debit on (SKA1_GVTYP_BSEG<> "" OR  BSEG_KOART = S) AND NOT(ZF_BSEG_HKONT_PAYFLAG) 
							Khoi: Not found in AP clearing and Not found in AR clearing
							Credit on BSEG_KOART = K
						  to Supplier invoice
			*/
			UPDATE B07_01_TT_FIN_AP_INV_PAY_FLAGS
			SET ZF_FLAG_SUMMARY = 'Supplier invoice', ZF_SUPP_INV = 'X', 
			    UNCLASSIFIED_FLAG = ''
			WHERE UNCLASSIFIED_FLAG = 'Y' AND
				  (
					LEFT(ZF_DEBIT_SKA1_GVTYP_FLAG_LIST,1) <> '_' OR
					LEFT(ZF_DEBIT_ACCOUNT_TYPES, 1) = 'S'
				  )
				  AND  ZF_DEBIT_FIRST_ACCOUNT_IS_BANK_OR_CLEAR<>'Y' --KHOI
	              AND ZF_DEBIT_FIRST_ACCOUNT_AR_CLEARING<>'Y' 
				  AND LEFT(ZF_CREDIT_ACCOUNT_TYPES, 1) = 'K'



			/*
				Step 3.12: Flag all documents have
							Debit on account type 'K'
							Credit on (SKA1_GVTYP_BSEG<> "" OR  BSEG_KOART = S) AND NOT(ZF_BSEG_HKONT_PAYFLAG) 
							Khoi: Not found in AP clearing
						  to Supplier invoice cancellation
			*/
			UPDATE B07_01_TT_FIN_AP_INV_PAY_FLAGS
			SET ZF_FLAG_SUMMARY = 'Supplier invoice cancellation', ZF_SUPP_INV_CANC = 'X', 
			    UNCLASSIFIED_FLAG = ''
			WHERE UNCLASSIFIED_FLAG = 'Y' AND
			LEFT(ZF_DEBIT_ACCOUNT_TYPES, 1) = 'K' AND
			(
				LEFT(ZF_CREDIT_SKA1_GVTYP_FLAG_LIST, 1) <> '_' OR
				LEFT(ZF_CREDIT_ACCOUNT_TYPES, 1) = 'S'
			)
			AND  ZF_CREDIT_FIRST_ACCOUNT_IS_BANK_OR_CLEAR<>'Y' --KHOI

			
			/*
				Step 3.13: Flag the rest documents to others
			*/
			UPDATE B07_01_TT_FIN_AP_INV_PAY_FLAGS
			SET  ZF_OTHERS = 'X'
			WHERE ZF_FLAG_SUMMARY = 'Others'


	  /*
			Step 4: Join the summary table after flaged all AP docuement to categories back to full AP document cube.
	  */
		EXEC SP_REMOVE_TABLES 'B07_03_IT_FIN_AP_INV_PAY_FLAGS'
		SELECT 
			B07_00_TT_AP_ADD_EXTRA_FLAG.*,
			B07_01_TT_FIN_AP_INV_PAY_FLAGS.ZF_FLAG_SUMMARY,
			B07_01_TT_FIN_AP_INV_PAY_FLAGS.ZF_SUPP_INV,
			B07_01_TT_FIN_AP_INV_PAY_FLAGS.ZF_SUPP_INV_CANC,
			B07_01_TT_FIN_AP_INV_PAY_FLAGS.ZF_SUPP_PAY,
			B07_01_TT_FIN_AP_INV_PAY_FLAGS.ZF_SUPP_PAY_CANC,
			B07_01_TT_FIN_AP_INV_PAY_FLAGS.ZF_OTHERS
		INTO B07_03_IT_FIN_AP_INV_PAY_FLAGS
		FROM B07_00_TT_AP_ADD_EXTRA_FLAG
		LEFT JOIN B07_01_TT_FIN_AP_INV_PAY_FLAGS
		ON 
		 ISNULL(B07_00_TT_AP_ADD_EXTRA_FLAG.BKPF_TCODE, '') = ISNULL(B07_01_TT_FIN_AP_INV_PAY_FLAGS.BKPF_TCODE, '') AND
		 ISNULL(B07_00_TT_AP_ADD_EXTRA_FLAG.ACDOCA_AWTYP, '') = ISNULL(B07_01_TT_FIN_AP_INV_PAY_FLAGS.ACDOCA_AWTYP, '') AND
		 ISNULL(B07_00_TT_AP_ADD_EXTRA_FLAG.LFA1_KTOKK, '') = ISNULL(B07_01_TT_FIN_AP_INV_PAY_FLAGS.LFA1_KTOKK, '') AND
		 ISNULL(B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_DEBIT_ACCOUNT_TYPES, '') = ISNULL(B07_01_TT_FIN_AP_INV_PAY_FLAGS.ZF_DEBIT_ACCOUNT_TYPES, '') AND
		 ISNULL(B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_CREDIT_ACCOUNT_TYPES, '') = ISNULL(B07_01_TT_FIN_AP_INV_PAY_FLAGS.ZF_CREDIT_ACCOUNT_TYPES, '') AND
		 ISNULL(B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_DEBIT_T012K_FEBKO_SKB1_HBKID_XGKON_FLAG_LIST, '') = ISNULL(B07_01_TT_FIN_AP_INV_PAY_FLAGS.ZF_DEBIT_T012K_FEBKO_SKB1_HBKID_XGKON_FLAG_LIST , '')AND 
		 ISNULL(B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_CREDIT_T012K_FEBKO_SKB1_HBKID_XGKON_FLAG_LIST, '') = ISNULL(B07_01_TT_FIN_AP_INV_PAY_FLAGS.ZF_CREDIT_T012K_FEBKO_SKB1_HBKID_XGKON_FLAG_LIST, '') AND
		 ISNULL(B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_DEBIT_SKA1_GVTYP_FLAG_LIST, '') = ISNULL(B07_01_TT_FIN_AP_INV_PAY_FLAGS.ZF_DEBIT_SKA1_GVTYP_FLAG_LIST, '') AND
		 ISNULL(B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_CREDIT_SKA1_GVTYP_FLAG_LIST, '') = ISNULL(B07_01_TT_FIN_AP_INV_PAY_FLAGS.ZF_CREDIT_SKA1_GVTYP_FLAG_LIST, '') AND
		 ISNULL(B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_DOC_CLEARING_FLAG, '') = ISNULL(B07_01_TT_FIN_AP_INV_PAY_FLAGS.ZF_DOC_CLEARING_FLAG, '') AND
		 ISNULL(B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_REGUH_PAYMENT_FLAG, '') = ISNULL(B07_01_TT_FIN_AP_INV_PAY_FLAGS.ZF_REGUH_PAYMENT_FLAG, '') AND 
		 ISNULL(B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_REGUP_INVOICE_FLAG, '') = ISNULL(B07_01_TT_FIN_AP_INV_PAY_FLAGS.ZF_REGUP_INVOICE_FLAG, '') AND 
		 ISNULL(B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_LIFNR_KUNNR_IS_SAME_FLAG, '') = ISNULL(B07_01_TT_FIN_AP_INV_PAY_FLAGS.ZF_LIFNR_KUNNR_IS_SAME_FLAG, '') AND
		 ISNULL(B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_DEBIT_FIRST_ACCOUNT_AP_CLEARING, '') = ISNULL(B07_01_TT_FIN_AP_INV_PAY_FLAGS.ZF_DEBIT_FIRST_ACCOUNT_AP_CLEARING, '') AND 
		 ISNULL(B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_CREDIT_FIRST_ACCOUNT_AP_CLEARING, '') = ISNULL(B07_01_TT_FIN_AP_INV_PAY_FLAGS.ZF_CREDIT_FIRST_ACCOUNT_AP_CLEARING, '') AND
		 ISNULL(B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_DEBIT_FIRST_ACCOUNT_IS_BANK_OR_CLEAR, '') = ISNULL(B07_01_TT_FIN_AP_INV_PAY_FLAGS.ZF_DEBIT_FIRST_ACCOUNT_IS_BANK_OR_CLEAR, '') AND 
		 ISNULL(B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_CREDIT_FIRST_ACCOUNT_IS_BANK_OR_CLEAR, '') = ISNULL(B07_01_TT_FIN_AP_INV_PAY_FLAGS.ZF_CREDIT_FIRST_ACCOUNT_IS_BANK_OR_CLEAR, '') AND
    	 ISNULL(B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_DEBIT_FIRST_ACCOUNT_AR_CLEARING, '') = ISNULL(B07_01_TT_FIN_AP_INV_PAY_FLAGS.ZF_DEBIT_FIRST_ACCOUNT_AR_CLEARING, '') AND 
		 ISNULL(B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_CREDIT_FIRST_ACCOUNT_AR_CLEARING, '') = ISNULL(B07_01_TT_FIN_AP_INV_PAY_FLAGS.ZF_CREDIT_FIRST_ACCOUNT_AR_CLEARING, '') AND 
    	 ISNULL(B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_FIRST_CREDIT_ACCOUNT, '') = ISNULL(B07_01_TT_FIN_AP_INV_PAY_FLAGS.ZF_FIRST_CREDIT_ACCOUNT, '') AND 
		 ISNULL(B07_00_TT_AP_ADD_EXTRA_FLAG.ZF_FIRST_DEBIT_ACCOUNT, '') = ISNULL(B07_01_TT_FIN_AP_INV_PAY_FLAGS.ZF_FIRST_DEBIT_ACCOUNT, '') 

EXEC SP_DROPTABLE B07_00_TT_AP_ADD_EXTRA_FLAG
EXEC SP_RENAME_FIELD 'B07B_', 'B07_03_IT_FIN_AP_INV_PAY_FLAGS'
EXEC SP_REMOVE_TABLES '%_TT_%'


GO
