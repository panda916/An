USE [DIVA_MASTER_SCRIPT_S4HANA]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO



----ALTER PROCEDURE [dbo].[B11_PTP_APA] 
CREATE   PROCEDURE [dbo].[script_B11_PTP_APA_BK]
WITH EXECUTE AS CALLER
AS
--DYNAMIC_SCRIPT_START


/* Initiate the log */  
--Create database log table if it does not exist
IF OBJECT_ID('LOG_SP_EXECUTION', 'U') IS NULL BEGIN CREATE TABLE [DBO].[LOG_SP_EXECUTION] ([DATABASE] NVARCHAR(MAX) NULL,[OBJECT] NVARCHAR(MAX) NULL,[OBJECT_TYPE] NVARCHAR(MAX) NULL,[USER] NVARCHAR(MAX) NULL,[DATE] DATE NULL,[TIME] TIME NULL,[DESCRIPTION] NVARCHAR(MAX) NULL,[TABLE] NVARCHAR(MAX),[ROWS] INT) END

--Log start of procedure
INSERT INTO [DBO].[LOG_SP_EXECUTION] ([DATABASE],[OBJECT],[OBJECT_TYPE],[USER],[DATE],[TIME],[DESCRIPTION],[TABLE],[ROWS])
SELECT DB_NAME(),OBJECT_NAME(@@PROCID),'P',SYSTEM_USER,CONVERT(DATE,GETDATE()),CONVERT(TIME,GETDATE()),'Procedure started',NULL,NULL

	/* Initialize parameters from globals table */
    DECLARE  
				@CURRENCY NVARCHAR(3)                 = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'currency')
				,@DATE1 NVARCHAR(MAX)                           = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'date1')
				,@DATE2 NVARCHAR(MAX)                           = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'date2')
				,@DOWNLOADDATE NVARCHAR(MAX)             = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'downloaddate')
				,@DATEFORMAT VARCHAR(3)             = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'dateformat')
				,@EXCHANGERATETYPE NVARCHAR(MAX)  = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'exchangeratetype')
				,@LANGUAGE1 NVARCHAR(3)                = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'language1')
				,@LANGUAGE2 NVARCHAR(3)                = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'language2')
				,@LIMIT_RECORDS INT                    = CAST((SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'LIMIT_RECORDS') AS INT)
				,@FISCAL_YEAR_FROM NVARCHAR(MAX)					= (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'FISCAL_YEAR_FROM')
				,@FISCAL_YEAR_TO NVARCHAR(MAX)					= (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'FISCAL_YEAR_TO')
                 SET DATEFORMAT @DATEFORMAT;


/*Test mode*/

SET ROWCOUNT @LIMIT_RECORDS

/*Change history comments*/


  /*
  Title:	B11_PTP_APA Accounts payable
  Description: Generates up to three cubes:
		- APA - Accounts Payable line items
		- APA-new - Accounts Payable line items incorporating new GL data. Dependent on APA and GLA-new
		- APA-lines - Counter GL line items for accounts payable (e.g. for and invoice that Cr Accounts Payable, this cube will give the corresponding debit line items)

    --------------------------------------------------------------
    Update history
    --------------------------------------------------------------
    Date		    | Who |	Description

	13-04-2016		  MW	First version for Sony  
	18-04-2016		  EH	Added back in ZF_Bucket - AP as this is required for overview routine
	04-05-2016		  EH    Added index on BSAK and added code to remove and then re-add BKPF PK index as this was clashing with BSIK_BSAK_BKPF and impacting performance
	06-02-2017		  JSM	Added database log table for the stored procedure and commented all the index query.
	19-03-2017		  CW    Update and standardisation for SID
	19-03-2017		  CW    Remove manual parameters to enable running from ETL 13
	28-07-2017        NP    Naming convention
	28-07-2017        Anh   Integration of information necessary for audit tests 
	05-08-2019		  VL	Update with S4HANA logic
	24-03-2022	     Thuan	Remove MANDT field in join
  */

 
--Step 1/ Create a list of MM invoices with a flag to show if there was a purchase order
		--   and a flag to show if there was a goods receipt flags to show if there was a purchase order
		EXEC SP_REMOVE_TABLES 'B11_01_TT_RSEG'
	  -- Obtain information concerning the MM invoice
		SELECT    
		  A_RSEG.RSEG_MANDT,
		  A_RSEG.RSEG_GJAHR,      
		  A_RSEG.RSEG_BELNR,
		  MAX(CASE WHEN COALESCE(B09_13_IT_PTP_POS.B09_EKKO_EBELN,'')<>'' THEN 'X' ELSE '' END) AS ZF_RSEG_INV_WITH_PO,
		  MAX(COALESCE(B09_13_IT_PTP_POS.B09_ZF_EKBE_VGABE_IS_A_GR,''))							AS ZF_RSEG_INV_WITH_GR
		
		INTO B11_01_TT_RSEG
		
		FROM A_RSEG
		
		LEFT JOIN B09_13_IT_PTP_POS 
		  ON  A_RSEG.RSEG_EBELN = B09_13_IT_PTP_POS.B09_EKKO_EBELN
		  AND A_RSEG.RSEG_EBELP = B09_13_IT_PTP_POS.B09_EKPO_EBELP
		
		GROUP BY 
			A_RSEG.RSEG_MANDT, 
			A_RSEG.RSEG_GJAHR, 
			A_RSEG.RSEG_BELNR

/*--Step 2/
-- Create a list of journal entry numbers for which there is more than one supplier
*/


	EXEC SP_REMOVE_TABLES'B11_02_TT_JE_MULTIPLE_SUPP'
	SELECT 
		ACDOCA_GJAHR
		,ACDOCA_RBUKRS
		,ACDOCA_BELNR
		,COUNT(DISTINCT ACDOCA_LIFNR) AS ZF_ACDOCA_NUM_SUPP_PER_JE
		INTO B11_02_TT_JE_MULTIPLE_SUPP
		FROM B04_11_IT_AP_ACC_SCH
		GROUP BY ACDOCA_GJAHR, ACDOCA_RBUKRS, ACDOCA_BELNR
		HAVING COUNT(DISTINCT ACDOCA_LIFNR)>1


/*--Step 3
-- Create a list of supplier journal entry lines that are enriched with information from above tables and other tables 
	- Only keep journal entries lines for which the status is not pre-posting, noted or parked (BSAK_BSTAT = '')
	- -- Only keep items that are posted in the period (BUDAT between date1 and date2
-- Note: in this step, all lines are kept, whether they are for invoices, payments, etc. as these
   are required for the red flags.
   In Step 7, the table is split into buckets for invoices, payments, etc.
--Fields are being added from other SAP tables as mentioned in JOIN clauses below
--Fields are being calculated as mentioned in SELECT clause below*/ 


	EXEC SP_REMOVE_TABLES 'B11_03_TT_PTP_APA'
	SELECT 	
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_RCLNT AS  ACDOCA_RCLNT,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_RBUKRS AS  ACDOCA_RBUKRS,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_GJAHR AS  ACDOCA_GJAHR,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_BELNR AS  ACDOCA_BELNR,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_BUZEI AS  ACDOCA_BUZEI,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_DOCLN AS  ACDOCA_DOCLN,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_BSCHL AS  ACDOCA_BSCHL,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_DRCRK AS  ACDOCA_DRCRK,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_BLART AS  ACDOCA_BLART,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_LIFNR AS  ACDOCA_LIFNR,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_RWCUR AS  ACDOCA_RWCUR,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_RHCUR AS  ACDOCA_RHCUR,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_RKCUR AS  ACDOCA_RKCUR,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_ROCUR AS  ACDOCA_ROCUR,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_HSL AS  ACDOCA_HSL,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_KSL AS  ACDOCA_KSL,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_OSL AS  ACDOCA_OSL,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_UMSKZ AS  ACDOCA_UMSKZ,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_AUGDT AS  ACDOCA_AUGDT,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_AUGBL AS  ACDOCA_AUGBL,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_ZUONR AS  ACDOCA_ZUONR,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_BUDAT AS  ACDOCA_BUDAT,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_BLDAT AS  ACDOCA_BLDAT,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_CPUDT AS  ACDOCA_CPUDT,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_BKPF_XBLNR AS  ACDOCA_XBLNR,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_MONAT AS  ACDOCA_MONAT,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_RBUSA AS  ACDOCA_RBUSA,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_WSL AS  ACDOCA_WSL,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_MWSKZ AS  ACDOCA_MWSKZ,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_SGTXT AS  ACDOCA_SGTXT,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_AUFNR AS  ACDOCA_AUFNR,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_EBELN AS  ACDOCA_EBELN,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_EBELP AS  ACDOCA_EBELP,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_RACCT AS  ACDOCA_RACCT,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_ZFBDT AS  ACDOCA_ZFBDT,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_ZTERM AS  ACDOCA_ZTERM,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_ZBD1T AS  ACDOCA_ZBD1T,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_ZBD2T AS  ACDOCA_ZBD2T,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_ZBD3T AS  ACDOCA_ZBD3T,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_ZBD1P AS  ACDOCA_ZBD1P,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_ZBD2P AS  ACDOCA_ZBD2P,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_SKFBT AS  ACDOCA_SKFBT,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_WSKTO AS  ACDOCA_WSKTO,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_BSTAT AS  ACDOCA_BSTAT,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_PS_POSID AS  ACDOCA_PS_POSID,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_RCNTR AS  ACDOCA_RCNTR,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_PRCTR AS  ACDOCA_PRCTR,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_AUGGJ AS  ACDOCA_AUGGJ,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ZF_ACDOCA_CPUDT_AGE_DAYS AS  ZF_ACDOCA_CPUDT_AGE_DAYS,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ZF_ACDOCA_OPEN_CLOSED AS  ZF_ACDOCA_OPEN_CLOSED,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ZF_ACDOCA_DRCRK_DESC AS  ZF_ACDOCA_DRCRK_DESC,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ZF_ACDOCA_DRCRK_INTEGER AS  ZF_ACDOCA_DRCRK_INTEGER,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ZF_ACDOCA_AUGDT_YEAR_MNTH AS  ZF_ACDOCA_AUGDT_YEAR_MNTH,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ZF_ACDOCA_WSKTO_SKBFT AS  ZF_ACDOCA_WSKTO_SKBFT,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_T001_WAERS AS  T001_WAERS,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ZF_ACDOCA_HSL_S AS  ZF_ACDOCA_HSL_S,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ZF_ACDOCA_KSL_S AS  ZF_ACDOCA_KSL_S,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ZF_ACDOCA_OSL_S AS  ZF_ACDOCA_OSL_S,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_GLOBALS_CURRENCY AS  GLOBALS_CURRENCY,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ZF_ACDOCA_HSL_S_CUC AS  ZF_ACDOCA_HSL_S_CUC,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_LFA1_KTOKK AS  LFA1_KTOKK,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_T077Y_TXT30 AS  T077Y_TXT30,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_T003T_LTEXT AS  T003T_LTEXT,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_BKPF_TCODE AS  BKPF_TCODE,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_TSTCT_TTEXT AS  TSTCT_TTEXT,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ZF_DEBIT_ACCOUNTS AS  ZF_DEBIT_ACCOUNTS,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ZF_CREDIT_ACCOUNTS AS  ZF_CREDIT_ACCOUNTS,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ZF_DEBIT_ACCOUNT_TEXTS AS  ZF_DEBIT_ACCOUNT_TEXTS,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ZF_CREDIT_ACCOUNT_TEXTS AS  ZF_CREDIT_ACCOUNT_TEXTS,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_RCNTR AS  ZF_ACDOCA_RCNTR,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ZF_ACDOCA_CSKT_LTEXT AS  ZF_CSKT_LTEXT,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ZF_SUPP_INV AS  ZF_SUPP_INV,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ZF_SUPP_INV_CANC AS  ZF_SUPP_INV_CANC,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ZF_SUPP_PAY AS  ZF_SUPP_PAY,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ZF_SUPP_PAY_CANC AS  ZF_SUPP_PAY_CANC,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ZF_OTHERS AS ZF_OTHERS,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ZF_FLAG_SUMMARY ZF_FLAG_SUMMARY,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_KOKRS AS ACDOCA_KOKRS,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_XANET AS ACDOCA_XANET,
		ACDOCA_KOART,
		A_BSEG.BSEG_XRAGL ACDOCA_XRAGL,
		--Business area text description
		A_TGSBT.TGSBT_GTEXT,
		-- Business domaines
		AM_SCOPE.SCOPE_BUSINESS_DMN_L1,
		AM_SCOPE.SCOPE_BUSINESS_DMN_L2,
		--Company code description
		A_T001.T001_BUTXT,
		A_T001.T001_LAND1,
		-- Supplier details
		B08_02_IT_PTP_SMD.B08_LFA1_NAME1 AS LFA1_NAME1,
		B08_02_IT_PTP_SMD.B08_LFA1_LAND1 AS LFA1_LAND1,
		B08_02_IT_PTP_SMD.B08_LFA1_VBUND AS LFA1_VBUND,
		B08_02_IT_PTP_SMD.B08_INTERCO_TXT as INTERCO_TXT,
		--Includes document status texts
		COALESCE(A_DD07T.DD07T_DDTEXT, '') AS DD07T_DDTEXT,
		-- Three digit country code needed for dashboards. Field above is SAP field that is the two character code
		AM_COUNTRY_MAPPING_LFA1.COUNTRY_MAPPING_DESC,
		AM_COUNTRY_MAPPING_T001.COUNTRY_MAPPING_DESC T001_COUNTRY_MAPPING_DESC,
		-- Posting key description
		B00_TBSLT.TBSLT_LTEXT, 
		-- Values are inversed in the _S fields - so that the total spend shows as positive on dashboard
		CONVERT(MONEY,B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_WSL * COALESCE(TCURX_DOC.TCURX_FACTOR,1)) AS ZF_ACDOCA_WSL_S,
		B07B_ZF_ACDOCA_KSL_S AS ZF_ACDOCA_WSL_S_CUC,
		B07B_ZF_ACDOCA_KSL_S AS ZF_ACDOCA_HSL_CUC,
		-- User information
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_USNAM AS ACDOCA_USNAM, 
		A_V_USERNAME.V_USERNAME_NAME_TEXT,      
		B00_USR02.USR02_USTYP,
		CASE 
		 WHEN B00_USR02.USR02_USTYP = 'A' THEN 'Dialog' 
		 WHEN B00_USR02.USR02_USTYP = 'B' THEN 'System' 
		 WHEN B00_USR02.USR02_USTYP = 'C' THEN 'Communication (external RFC)' 
		 WHEN B00_USR02.USR02_USTYP = 'L' THEN 'Reference' 
		 WHEN B00_USR02.USR02_USTYP = 'S' THEN 'Service' 
		 ELSE 'Other' 
		END AS ZF_USR02_USTYP_DESC,		
        -- GL account description for supplier control account
		B00_SKAT.SKAT_TXT50, 
		-- Profit center information
		COALESCE(CASE 
			WHEN B00_CEPCT.CEPCT_LTEXT ='' OR  B00_CEPCT.CEPCT_LTEXT IS NULL THEN 'Not assigned'
			ELSE  B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_PRCTR 
		END,'Not assigned')																					AS ZF_ACDOCA_PRCTR,			
		COALESCE(CASE 
			WHEN B00_CEPCT.CEPCT_LTEXT ='' OR  B00_CEPCT.CEPCT_LTEXT IS NULL THEN 'Not assigned'
			ELSE  B00_CEPCT.CEPCT_LTEXT 
		END,'Not assigned')																					AS ZF_CEPCT_LTEXT,
		-- MM-invoice information
		CASE 
			WHEN B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_AWTYP = 'RMRP' 
			THEN 'X' ELSE '' 
		END			
																							AS ZF_ACDOCA_AWTYP_MM_INV,
		CASE 
			WHEN B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_AWTYP = 'RMRP'  THEN  LEFT(A_BKPF.BKPF_AWKEY, 10) 
			ELSE '' 
		END																								AS ZF_ACDOCA_AWTYP_MM_INV_NUM,
		CASE 
			WHEN B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_AWTYP = 'RMRP'  THEN RIGHT(A_BKPF.BKPF_AWKEY,4)	 
			ELSE '' 
		END																								AS ZF_ACDOCA_AWTYP_MM_INV_YEAR,
		A_BKPF.BKPF_AWKEY									AS 		BKPF_AWKEY,		
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_AWTYP	AS 		ACDOCA_AWTYP,

		-- Date fields (calendar)
		YEAR(B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_BUDAT)														AS ZF_ACDOCA_BUDAT_CY,
		CAST(YEAR(B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_BUDAT) AS VARCHAR(4)) + '-' + RIGHT('0' + CAST(MONTH(B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_BUDAT) AS VARCHAR(2)),2)		AS ZF_ACDOCA_BUDAT_CY_MNTH,
		CAST(YEAR(B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_BUDAT) AS VARCHAR(4)) + '-' +
			CASE 
				WHEN MONTH (B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_BUDAT) IN (4,5,6) THEN 'Q2'
				WHEN MONTH (B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_BUDAT) IN (7,8,9) THEN 'Q3'
				WHEN MONTH (B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_BUDAT) IN (10,11,12) THEN 'Q4'
				ELSE 'Q1' 
			END																								AS ZF_ACDOCA_BUDAT_CY_CQ,
		
		-- Date fields (fiscal)
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_GJAHR + '-' + CASE WHEN B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_MONAT IN ('01','02','03') then 'Q1'
						   WHEN B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_MONAT IN ('04','05','06') then 'Q2'
						   WHEN B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_MONAT IN ('07','08','09') then 'Q3'
						   WHEN B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_MONAT IN ('10','11','12') then 'Q4'
					  ELSE 'Q4' END																			AS ZF_ACDOCA_MONAT_FQ,
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_GJAHR + '-' + B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_MONAT						AS ZF_ACDOCA_GJAHR_MONAT,
		-- Logic to create 3 character posting month names.
		CONVERT(VARCHAR(3), DATENAME(MM, B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_BUDAT))								AS ZF_ACDOCA_BUDAT_MNTH,
		-- Entry time
		B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_CPUTM AS ACDOCA_CPUTM,
		-- Journal entry header text
		A_BKPF.BKPF_BKTXT AS BKPF_BKTXT,
		-- Determine what the last day of the period is
		EOMONTH(B07B_ACDOCA_AUGDT) AS ZF_ACDOCA_AUGDT_LST_DY_CAL,

		-- Take whatever is earlier, the last day of the period or the matching da0te
		YEAR(B07B_ACDOCA_AUGDT)+ '-' +MONTH(B07B_ACDOCA_AUGDT)  AS ZF_ACDOCA_AUGDT_CAL_MNTH,
		
		-- Early and late payment buckets
		CASE 
			WHEN B07B_ZF_ACDOCA_CPUDT_AGE_DAYS < 0 THEN '<0' 
			WHEN B07B_ZF_ACDOCA_CPUDT_AGE_DAYS BETWEEN 0 AND 30 THEN '0-30'  
			WHEN B07B_ZF_ACDOCA_CPUDT_AGE_DAYS BETWEEN 31 AND 60 THEN '31-60' 
			WHEN B07B_ZF_ACDOCA_CPUDT_AGE_DAYS BETWEEN 61 AND 90 THEN '61-90'
			WHEN B07B_ZF_ACDOCA_CPUDT_AGE_DAYS BETWEEN 91 AND 120 THEN '91-120'
			WHEN B07B_ZF_ACDOCA_CPUDT_AGE_DAYS BETWEEN 121 AND 180 THEN '121-180'
			WHEN B07B_ZF_ACDOCA_CPUDT_AGE_DAYS BETWEEN 181 AND 360 THEN '181-360'
			ELSE '+360'         
		END ZF_ACDOCA_CPUDT_AGE_DAY_RANGE, 

		DATEDIFF(DD, B07B_ACDOCA_AUGDT, B07B_ACDOCA_BLDAT) ZF_BLDAT_MINUS_AUGDT,
		IIF(DATEDIFF(DD, B07B_ACDOCA_AUGDT, B07B_ACDOCA_BLDAT) > 0,
		CASE
			WHEN DATEDIFF(DD, B07B_ACDOCA_AUGDT, B07B_ACDOCA_BLDAT) <= 10 THEN '01 - 10'
			WHEN DATEDIFF(DD, B07B_ACDOCA_AUGDT, B07B_ACDOCA_BLDAT) <= 20 THEN '11 - 20'
			WHEN DATEDIFF(DD, B07B_ACDOCA_AUGDT, B07B_ACDOCA_BLDAT) <= 30 THEN '21 - 30'
			ELSE '> 30' END + 'days', NULL) ZF_BLDAT_MINUS_AUGDT_BUCKET,
		IIF(DATEDIFF(DD, B07B_ACDOCA_AUGDT, B07B_ACDOCA_BLDAT) > 0, 1 , 0) ZF_BLDAT_MINUS_AUGDT_FLAG,
		
		-- Calculated due date
		DATEADD(d, 
		  CASE 
			WHEN (B07B_ACDOCA_ZBD2T <> 0) AND (B07B_ZF_ACDOCA_WSKTO_SKBFT <> B07B_ACDOCA_ZBD1P) THEN
			  CASE 
				WHEN (B07B_ZF_ACDOCA_WSKTO_SKBFT = B07B_ACDOCA_ZBD2P) THEN B07B_ACDOCA_ZBD2T
				WHEN (B07B_ACDOCA_ZBD3T <> 0) THEN B07B_ACDOCA_ZBD3T
				ELSE B07B_ACDOCA_ZBD1T 
			  END
			ELSE B07B_ACDOCA_ZBD1T 
		  END, B07B_ACDOCA_ZFBDT) ZF_ACDOCA_ZBDXT_DUE_DATE,

		-- Days late, based on calculated due date logic compared against clearing date
		DATEDIFF(d,
			DATEADD(d, 
				CASE 
					WHEN (B07B_ACDOCA_ZBD2T <> 0) AND (B07B_ZF_ACDOCA_WSKTO_SKBFT <> B07B_ACDOCA_ZBD1P) THEN
						CASE 
							WHEN (B07B_ZF_ACDOCA_WSKTO_SKBFT = B07B_ACDOCA_ZBD2P) THEN B07B_ACDOCA_ZBD2T
							WHEN	(B07B_ACDOCA_ZBD3T <> 0) THEN B07B_ACDOCA_ZBD3T
							ELSE B07B_ACDOCA_ZBD1T 
						END
					ELSE B07B_ACDOCA_ZBD1T 
				END, B07B_ACDOCA_ZFBDT)

			,CASE 
				WHEN COALESCE(B07B_ACDOCA_AUGBL, '') = '' THEN NULL 
				ELSE B07B_ACDOCA_AUGDT 
			END) ZF_ACDOCA_AUGDT_MINUS_ZBDXT,

		-- Days late, based on document date later than posting date
		DATEDIFF(d, B07B_ACDOCA_BLDAT, B07B_ACDOCA_BUDAT) AS ZF_ACDOCA_BUDAT_MINUS_BLDAT,
		-- Planned DPO, based on comparison between baseline date and calculated due date
		CASE 
			WHEN B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_HSL = 0 THEN 0 
			ELSE datediff(dd,		  
				--Baseline date
				B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_ZFBDT, 
			
			  --Calc due date
			  DATEADD(d, 
				CASE 
					WHEN (B07B_ACDOCA_ZBD2T <> 0) AND (B07B_ZF_ACDOCA_WSKTO_SKBFT <> B07B_ACDOCA_ZBD1P) THEN
						CASE 
							WHEN (B07B_ZF_ACDOCA_WSKTO_SKBFT = B07B_ACDOCA_ZBD2P) THEN B07B_ACDOCA_ZBD2T
							WHEN (B07B_ACDOCA_ZBD3T <> 0) THEN B07B_ACDOCA_ZBD3T
							ELSE B07B_ACDOCA_ZBD1T 
						END
					ELSE B07B_ACDOCA_ZBD1T 
				END, B07B_ACDOCA_ZFBDT) 
		  ) 
		 END ZF_ACDOCA_ZFBDT_MINUS_ZBDXT,

		-- Actual DPO, based on difference between baseline date and clearing date
		CASE 
			WHEN B07B_ACDOCA_HSL = 0 THEN 0 
			ELSE DATEDIFF(DD, B07B_ACDOCA_ZFBDT, B07B_ACDOCA_AUGDT) 
		END	ZF_ACDOCA_ZFBDT_MINUS_AUGDT,

		
		-- Calculation of whether payment of the invoice was early/late
		-- Days late is equivalent to difference between planned DPO and actual DPO. Calculated using the same logic as the days late field above
		CASE 
			WHEN (DATEDIFF(D,  
					DATEADD(D, 
					   CASE 
						  WHEN (B07B_ACDOCA_ZBD2T <> 0) AND (B07B_ZF_ACDOCA_WSKTO_SKBFT <> B07B_ACDOCA_ZBD1P)  THEN
							CASE 
								WHEN (B07B_ZF_ACDOCA_WSKTO_SKBFT = B07B_ACDOCA_ZBD2P) THEN B07B_ACDOCA_ZBD2T
								WHEN (B07B_ACDOCA_ZBD3T <> 0) THEN B07B_ACDOCA_ZBD3T
								ELSE B07B_ACDOCA_ZBD1T 
							END
						  ELSE B07B_ACDOCA_ZBD1T 
					   END, B07B_ACDOCA_ZFBDT)			
				,CASE 
					WHEN COALESCE(B07B_ACDOCA_AUGBL, '') = '' THEN NULL 
					ELSE B07B_ACDOCA_AUGDT 
				END)) < 0 THEN 'Early Payment'	  
			  
			WHEN (DATEDIFF(D,  
					DATEADD(D, 
					   CASE 
						  WHEN (B07B_ACDOCA_ZBD2T <> 0) AND (B07B_ZF_ACDOCA_WSKTO_SKBFT <> B07B_ACDOCA_ZBD1P) THEN
							 CASE 
								WHEN (B07B_ZF_ACDOCA_WSKTO_SKBFT = B07B_ACDOCA_ZBD2P) THEN B07B_ACDOCA_ZBD2T
								WHEN (B07B_ACDOCA_ZBD3T <> 0) THEN B07B_ACDOCA_ZBD3T
								ELSE B07B_ACDOCA_ZBD1T 
							 END
						  ELSE B07B_ACDOCA_ZBD1T 
					   END, B07B_ACDOCA_ZFBDT)			
				,CASE 
					WHEN COALESCE(B07B_ACDOCA_AUGBL, '') = '' THEN NULL 
					ELSE B07B_ACDOCA_AUGDT 
				 END)) = 0 THEN 'On-time Payment'	  
			  
			ELSE 'Late Payment' 
		END ZF_ACDOCA_ZBDXT_AUGDT_EARLY_OR_LATE_DESC,


		-- Other supplier master fields
		COALESCE (B08_02_IT_PTP_SMD.B08_LFA1_VBUND,'Unknown – supplier master record not available') ZF_LFA1_VBUND,
			
		-- Add indicator to show if there was an invoice with PO 
		COALESCE(B11_01_TT_RSEG.ZF_RSEG_INV_WITH_PO,'')	ZF_RSEG_INV_WITH_PO,
		-- Add indicator to show if there was an invoice with GR
		COALESCE(B11_01_TT_RSEG.ZF_RSEG_INV_WITH_GR,'')	ZF_RSEG_INV_WITH_GR,
		-- Add indicator to show if posted in the period
		CASE 
			WHEN B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_BUDAT >= @DATE1 AND B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_BUDAT <= @DATE2 THEN 'X'
			ELSE ''
		END	ZF_ACDOCA_BUDAT_POST_PER,
		-- Add indicator to show if cleared in the period
		CASE
			WHEN B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_AUGDT between @DATE1 and @DATE2 THEN 'X'
			ELSE ''
		END ZF_ACDOCA_AUGDT_CLEAR_PER,  
		-- Add a description of the document status
		CASE 
			WHEN COALESCE(A_T074U.T074U_MERKP,'') = '' THEN
				CASE B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_BSTAT
					WHEN 'V' THEN 'Parked'
					WHEN 'W' THEN 'Parked'
					WHEN 'Z' THEN 'Parked'
					WHEN 'S' THEN 'Noted'
					WHEN ''  THEN 'Normal'
					ELSE 'Other non-financial items'
				END
			ELSE 'Noted'      
		END	ZF_ACDOCA_BSTAT_DESC,
		-- Add an indicator to show if there are multiple suppliers per journal entry
		CASE WHEN (COALESCE(B11_02_TT_JE_MULTIPLE_SUPP.ACDOCA_BELNR,'') <> '')
				THEN 'X'
				ELSE ''
		END AS ZF_ACDOCA_MULTIPLE_SUPPS,
    	    	-- Invoice buckets 
		CASE 
		  WHEN B07B_ZF_SUPP_INV = 'X' THEN
			CASE
				WHEN B07B_ACDOCA_BUDAT < @DATE1 THEN 'Invoice - type unknown'
				WHEN (COALESCE(B11_01_TT_RSEG.ZF_RSEG_INV_WITH_PO,'') <> '') THEN      
					CASE 
						WHEN (COALESCE(B11_01_TT_RSEG.ZF_RSEG_INV_WITH_GR,'') <> '') THEN '3-way match invoice' 
						ELSE '2-way match invoice'
					END
				ELSE 'Direct invoice'
			END 
		  WHEN B07B_ZF_SUPP_INV_CANC = 'X' THEN 'Credit memo'   
		  ELSE 'N/A'  
	   END ZF_ACDOCA_INV_DESC,
		CASE                               
			WHEN B07B_ACDOCA_DRCRK = 'S'
				THEN B07B_ACDOCA_HSL 
			ELSE 0                                       
		 END AS ZF_ACDOCA_HSL_DEBIT                     
		,CASE                                            
			WHEN B07B_ACDOCA_DRCRK = 'H'
				THEN B07B_ACDOCA_HSL  
			ELSE 0                                        
		 END AS ZF_ACDOCA_HSL_CREDIT                     
		-- Add descriptions
		,B07B_ACDOCA_RBUKRS + '-' + A_T001.T001_BUTXT		AS ZF_ACDOCA_RBURKS_T001_BUTXT
		,B07B_ACDOCA_LIFNR  + '-' +B08_02_IT_PTP_SMD.B08_LFA1_NAME1 AS ZF_ACDOCA_LIFNR_NAME1
		,B00_T009B.T247_LTX ACDOCA_MONAT_DESC,
		T052U_AP.T052U_TEXT1 AS ZF_T052U_TEXT1_AP, -- Add document payment term description
		LFB1_ZTERM AS LFB1_ZTERM, -- Supplier payment term
		T052U_SU.T052U_TEXT1  AS ZF_T052U_TEXT1_SU  -- Supplier payment term description

	  INTO B11_03_TT_PTP_APA  
	  
	  FROM B07_03_IT_FIN_AP_INV_PAY_FLAGS

	    -- Get user name, transaction code, origin key, as well as some fields from ACDOCA
		LEFT JOIN A_BKPF
			ON  B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_RBUKRS = A_BKPF.BKPF_BUKRS AND    
			    B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_GJAHR = A_BKPF.BKPF_GJAHR AND 
			    B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_BELNR = A_BKPF.BKPF_BELNR

		-- Get reverse flag from BSEG
		LEFT JOIN A_BSEG
			ON	B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_RBUKRS = A_BSEG.BSEG_BUKRS AND    
			    B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_GJAHR = A_BSEG.BSEG_GJAHR AND 
			    B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_BELNR = A_BSEG.BSEG_BELNR AND
				B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_BUZEI = A_BSEG.BSEG_BUZEI
				
				
		LEFT JOIN B00_ACDOCA
			ON  B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_RBUKRS = B00_ACDOCA.ACDOCA_RBUKRS AND    
			    B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_GJAHR = B00_ACDOCA.ACDOCA_GJAHR AND 
			    B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_BELNR = B00_ACDOCA.ACDOCA_BELNR AND B00_ACDOCA.ACDOCA_KOART = 'K' AND
				B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_BUZEI = B00_ACDOCA.ACDOCA_BUZEI AND
				B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_DOCLN = B00_ACDOCA.ACDOCA_DOCLN

	   -- Add supplier master data details, including the supplier account gruop
		  LEFT JOIN B08_02_IT_PTP_SMD 
			ON B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_LIFNR = B08_02_IT_PTP_SMD.B08_EKKO_LIFNR

			
       -- Add the controlling area in order to be able to add profit center information		
		LEFT JOIN A_TKA02
			ON A_TKA02.TKA02_BUKRS = B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_RBUKRS

							       			
	    -- Adding below profit center and cost center descriptions
		-- But actually there are no profit centers or cost centers in BSAK for India
		--Don't have ACDOCA.KOKRS, so use TKA02 (mapping from BUKRS to KOKRS)
		LEFT JOIN B00_CEPCT 
			ON  A_TKA02.TKA02_KOKRS = B00_CEPCT.CEPCT_KOKRS
			AND B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_PRCTR = B00_CEPCT.CEPCT_PRCTR 
			AND B00_CEPCT.CEPCT_DATBI > @downloaddate
		
		-- Restrict to only the company codes in scope
		INNER JOIN AM_SCOPE 
			ON  B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_RBUKRS = AM_SCOPE.SCOPE_CMPNY_CODE 
	  
		-- Obtain chart of accounts code, company description and house currency
		LEFT JOIN A_T001 
			ON  B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_RBUKRS = A_T001.T001_BUKRS

		  -- Add chart of accounts description
		  LEFT JOIN B00_SKAT
			ON B00_SKAT.SKAT_KTOPL = A_T001.T001_KTOPL
			AND B00_SKAT.SKAT_SAKNR = B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_RACCT
		
		  -- Add currency conversion factor for document currency
		  LEFT JOIN B00_TCURX TCURX_DOC  
			
			ON B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_RWCUR = TCURX_DOC.TCURX_CURRKEY
		
		  -- Add currency conversion factors for company currency
		  LEFT JOIN B00_TCURX TCURX_CC      
			ON A_T001.T001_WAERS = TCURX_CC.TCURX_CURRKEY          
		
		  -- Add user name
		  LEFT JOIN A_V_USERNAME
			ON B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_USNAM = A_V_USERNAME.V_USERNAME_BNAME
		  
		  -- Add user account details
		  LEFT JOIN B00_USR02 
			ON B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_USNAM = B00_USR02.USR02_BNAME
		  
		  -- Add posting key details
		  LEFT JOIN B00_TBSLT
			ON B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_UMSKZ = B00_TBSLT.TBSLT_UMSKZ 
			AND B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_BSCHL = B00_TBSLT.TBSLT_BSCHL
		  
		  -- Add special ledger properties to determine if posting is to leading ledger
		  LEFT JOIN A_T074U
			ON ('K' = A_T074U.T074U_KOART) 
			AND (B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_UMSKZ = A_T074U.T074U_UMSKZ) 
		
		  -- Add MM invoice details
		  LEFT JOIN B11_01_TT_RSEG 
			ON B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_AWTYP = 'RMRP' 
			AND RIGHT(A_BKPF.BKPF_AWKEY,4) = B11_01_TT_RSEG.RSEG_GJAHR
			AND LEFT(A_BKPF.BKPF_AWKEY, 10) = B11_01_TT_RSEG.RSEG_BELNR


		-- Add 3 digit ISO country codes (for supplier dashboard maps)
		LEFT JOIN AM_COUNTRY_MAPPING AM_COUNTRY_MAPPING_LFA1
			ON AM_COUNTRY_MAPPING_LFA1.COUNTRY_MAPPING_CODE= B08_02_IT_PTP_SMD.B08_LFA1_LAND1

		-- Add 3 digit ISO country codes (for company dashboard maps)
		LEFT JOIN AM_COUNTRY_MAPPING AM_COUNTRY_MAPPING_T001
			ON AM_COUNTRY_MAPPING_T001.COUNTRY_MAPPING_CODE= A_T001.T001_LAND1

	    -- Add the multiple suppliers indicator
		LEFT JOIN B11_02_TT_JE_MULTIPLE_SUPP
			ON B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_GJAHR =B11_02_TT_JE_MULTIPLE_SUPP.ACDOCA_GJAHR
			AND B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_RBUKRS =B11_02_TT_JE_MULTIPLE_SUPP.ACDOCA_RBUKRS
			AND B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_BELNR =B11_02_TT_JE_MULTIPLE_SUPP.ACDOCA_BELNR

	    -- Add business area description
		LEFT JOIN A_TGSBT
		    ON B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_RBUSA = A_TGSBT.TGSBT_GSBER

		--Includes document status texts	
		LEFT JOIN A_DD07T 
		ON  (A_DD07T.DD07T_DDLANGUAGE = @language1)
			AND	(A_DD07T.DD07T_DOMNAME ='BSTAT') 
			AND	(B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_BSTAT = A_DD07T.DD07T_DOMVALUE_L)

		--Add posting period description
		LEFT JOIN B00_T009B
		ON CAST(B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_MONAT AS INT) = CAST(B00_T009B.T009B_POPER AS INT)
		AND A_T001.T001_PERIV = B00_T009B.T009B_PERIV

		-- Add supplier payment term
		LEFT JOIN A_LFB1
		ON B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_LIFNR = LFB1_LIFNR

		-- Add supplier payment term description
		LEFT JOIN B00_T052U T052U_SU
		ON LFB1_ZTERM = T052U_SU.T052U_ZTERM
		
		-- Add document payment term description
		LEFT JOIN B00_T052U T052U_AP
		ON B07_03_IT_FIN_AP_INV_PAY_FLAGS.B07B_ACDOCA_ZTERM = T052U_AP.T052U_ZTERM

				
/*-- Step 4/ 
-- Add additional fields to the cube that can not be added above because some calculated fields needed
   to be calculated first above
*/

   EXEC SP_REMOVE_TABLES 'B11_04_IT_PTP_APA'
   

	 SELECT B11_03_TT_PTP_APA.*
	    -- Add the days late range
		,CASE 
			WHEN ZF_ACDOCA_AUGDT_MINUS_ZBDXT < - 10 THEN '< -10' 
			WHEN ZF_ACDOCA_AUGDT_MINUS_ZBDXT < 0 THEN '-10 - -1'  
			WHEN ZF_ACDOCA_AUGDT_MINUS_ZBDXT <= 10 THEN '01 - 10'
			WHEN ZF_ACDOCA_AUGDT_MINUS_ZBDXT <= 20 THEN '11 - 20'
			WHEN ZF_ACDOCA_AUGDT_MINUS_ZBDXT <= 30 THEN '21 - 30'
			WHEN ZF_ACDOCA_AUGDT_MINUS_ZBDXT <= 60 THEN '31 - 60'
			WHEN ZF_ACDOCA_AUGDT_MINUS_ZBDXT <= 90 THEN '61 - 90'
			WHEN ZF_ACDOCA_AUGDT_MINUS_ZBDXT <= 120 THEN '91 - 120'
			WHEN ZF_ACDOCA_AUGDT_MINUS_ZBDXT > 120 THEN '121 +'
			ELSE	'No clearinng date or due date'
		END	AS ZF_ZBDXT_AUGDT_DAYS_LATE_RANGE
		,CASE 
				WHEN ZF_ACDOCA_BUDAT_MINUS_BLDAT < -10 THEN '< -10'
				WHEN ZF_ACDOCA_BUDAT_MINUS_BLDAT < 0 THEN '-10 - -1'
				WHEN ZF_ACDOCA_BUDAT_MINUS_BLDAT = 0 THEN '0'
				WHEN ZF_ACDOCA_BUDAT_MINUS_BLDAT <= 10 THEN '01 - 10'
				WHEN ZF_ACDOCA_BUDAT_MINUS_BLDAT <= 20 THEN '11 - 20'
				WHEN ZF_ACDOCA_BUDAT_MINUS_BLDAT <= 30 THEN '21 - 30'
				WHEN ZF_ACDOCA_BUDAT_MINUS_BLDAT <= 60 THEN '31 - 60'
				WHEN ZF_ACDOCA_BUDAT_MINUS_BLDAT <= 90 THEN '61 - 90'
				WHEN ZF_ACDOCA_BUDAT_MINUS_BLDAT <= 120 THEN '91 - 120'
				WHEN ZF_ACDOCA_BUDAT_MINUS_BLDAT > 120 THEN '121 +'
		END ZF_POSTING_BUDAT_MINUS_BLDAT_CATEGORY
		,CASE
				WHEN ZF_ACDOCA_AUGDT_MINUS_ZBDXT < -10 THEN '< -10'
				WHEN ZF_ACDOCA_AUGDT_MINUS_ZBDXT < 0 THEN '-10 - -1'
				WHEN ZF_ACDOCA_AUGDT_MINUS_ZBDXT = 0 THEN '0'
				WHEN ZF_ACDOCA_AUGDT_MINUS_ZBDXT <= 10 THEN '>0 - 10'
				WHEN ZF_ACDOCA_AUGDT_MINUS_ZBDXT <= 20 THEN '11 - 20'
				WHEN ZF_ACDOCA_AUGDT_MINUS_ZBDXT <= 30 THEN '21 - 30'
				WHEN ZF_ACDOCA_AUGDT_MINUS_ZBDXT <= 60 THEN '31 - 60'	
				WHEN ZF_ACDOCA_AUGDT_MINUS_ZBDXT <= 90 THEN '61 - 90'
				WHEN ZF_ACDOCA_AUGDT_MINUS_ZBDXT <= 120 THEN '91 - 120'
				WHEN ZF_ACDOCA_AUGDT_MINUS_ZBDXT > 120 THEN '121 +'
				ELSE 'No clearinng date or due date'
		END ZF_AUGDT_MINUS_ZBDXT_CATEGORY
		-- Add the value for early payments
		,CASE
			WHEN ZF_ACDOCA_ZBDXT_AUGDT_EARLY_OR_LATE_DESC = 'Early Payment'
				THEN ZF_ACDOCA_HSL_S_CUC
			ELSE 0
		 END AS ZF_EARLY_PAYMENT_VAL
		 -- Add the value for on-time payments
		,CASE
			WHEN ZF_ACDOCA_ZBDXT_AUGDT_EARLY_OR_LATE_DESC = 'On-time Payment'
				THEN ZF_ACDOCA_HSL_S_CUC
			ELSE 0
		 END AS ZF_ONTIME_PAYMENT_VAL
		 -- Add the value for late payments
		,CASE
			WHEN ZF_ACDOCA_ZBDXT_AUGDT_EARLY_OR_LATE_DESC = 'Late Payment'
				THEN ZF_ACDOCA_HSL_S_CUC
			ELSE 0
		 END AS ZF_LATE_PAYMENT_VAL		
		-- Add the debit/ credit amounts for custom currency
		,CASE                               
			WHEN ACDOCA_DRCRK = 'S'
				THEN ZF_ACDOCA_HSL_CUC
			ELSE 0                                       
		 END AS ZF_ACDOCA_HSL_CUC_DEBIT                     
		,CASE                                            
			WHEN ACDOCA_DRCRK = 'H'
				THEN ZF_ACDOCA_HSL_CUC
			ELSE 0                                        
		 END AS ZF_ACDOCA_HSL_CUC_CREDIT,               
		 
		 CONVERT(nvarchar(4), DATEPART(yyyy, ACDOCA_BUDAT)) + '-' + LEFT(DATENAME(mm, ACDOCA_BUDAT),3) ZF_ACDOCA_BUDAT_YYYYMMM,
		 CONVERT(DATE, CONVERT(NVARCHAR(4), DATEPART(yyyy, ACDOCA_BUDAT)) + 
		 
		 IIF(DATEPART(mm, ACDOCA_BUDAT) < 10, '0' + CONVERT(NVARCHAR(4), DATEPART(mm, ACDOCA_BUDAT)) , 
		 CONVERT(NVARCHAR(4), DATEPART(mm, ACDOCA_BUDAT))) + '01', 112) ZF_ACDOCA_BUDAT_YYYYMMDD_GROUPED

															
		INTO B11_04_IT_PTP_APA
		FROM B11_03_TT_PTP_APA

/*--Step 5
-- List of invoices and cancellations, invoices, cancellations, payments, payment cancellations
-- The list of invoices and cancellations is the list that is used in Qlik for PTP spendoverview
 */ 

 	   EXEC SP_REMOVE_TABLES 'B11_05_IT_PTP_INVS_AND_CANCS'
	 
       SELECT A.*
	   INTO B11_05_IT_PTP_INVS_AND_CANCS
	   FROM B11_04_IT_PTP_APA A
	   WHERE A.ZF_SUPP_INV = 'X' OR A.ZF_SUPP_INV_CANC = 'X'

       
	   EXEC SP_REMOVE_TABLES 'B11_06_IT_PTP_INV'
       SELECT * 
	   INTO B11_06_IT_PTP_INV
	   FROM B11_04_IT_PTP_APA
	   WHERE ZF_SUPP_INV = 'X'

	   EXEC SP_REMOVE_TABLES 'B11_07_IT_PTP_INV_CANC'

       SELECT * 
	   INTO B11_07_IT_PTP_INV_CANC
	   FROM B11_04_IT_PTP_APA
	   WHERE ZF_SUPP_INV_CANC = 'X'

	   EXEC SP_REMOVE_TABLES 'B11_08_IT_PTP_PAY'

       SELECT * 
	   INTO B11_08_IT_PTP_PAY
	   FROM B11_04_IT_PTP_APA
	   WHERE ZF_SUPP_PAY = 'X'

	   EXEC SP_REMOVE_TABLES 'B11_09_IT_PTP_PAY_CANC'

       SELECT * 
	   INTO B11_09_IT_PTP_PAY_CANC
	   FROM B11_04_IT_PTP_APA
	   WHERE ZF_SUPP_PAY_CANC = 'X'

-- Step 6 AP Ageing cube
-- Create a temp table with all month ends between @DATE1 and @DATE2
	DECLARE @DATELOW date = (SELECT CAST(@DATE1 AS date))
	DECLARE @DATEHIGH date = (SELECT CAST(@DATE2 AS date))

	EXEC SP_REMOVE_TABLES 'B11_11_TT_SNAPSHOT_DATE'
	CREATE TABLE B11_11_TT_SNAPSHOT_DATE (ZF_MNTH_END date)
	WHILE @DATELOW <= @DATEHIGH
	BEGIN 
		INSERT INTO B11_11_TT_SNAPSHOT_DATE 
		SELECT EOMONTH(@DATELOW)

		SET @DATELOW = DATEADD(mm, 1, @DATELOW)
	END

-- add compay code, mandant and fiscal year variant
	EXEC SP_REMOVE_TABLES 'B11_12_TT_SNAPSHOT_BUKRS'
	SELECT T001_MANDT, T001_BUKRS, T001_PERIV, ZF_MNTH_END 
	INTO B11_12_TT_SNAPSHOT_BUKRS 
	FROM A_T001
	INNER JOIN AM_SCOPE ON A_T001.T001_BUKRS = AM_SCOPE.SCOPE_CMPNY_CODE
	LEFT JOIN B11_11_TT_SNAPSHOT_DATE ON 1 = 1
	ORDER BY
		T001_BUKRS, ZF_MNTH_END ASC

	EXEC SP_REMOVE_TABLES 'B11_14_IT_AP_AGEING'
	SELECT
		B11_06_IT_PTP_INV.ACDOCA_RCLNT
		,B11_06_IT_PTP_INV.ACDOCA_RBUKRS
		,B11_06_IT_PTP_INV.ACDOCA_GJAHR
		,B11_06_IT_PTP_INV.ACDOCA_BELNR
		,B11_06_IT_PTP_INV.ACDOCA_BUZEI
		,B11_06_IT_PTP_INV.ACDOCA_DOCLN
		,B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END
		,B11_06_IT_PTP_INV.ACDOCA_AUGDT
		,B11_06_IT_PTP_INV.ACDOCA_LIFNR
		,B11_06_IT_PTP_INV.ACDOCA_XANET
		,DATEDIFF(dd, B11_06_IT_PTP_INV.ZF_ACDOCA_ZBDXT_DUE_DATE, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) ZF_ACDOCA_ZBDXT_DIFF
		,B11_06_IT_PTP_INV.ACDOCA_BLDAT -- Document date
		,DATEDIFF(dd, B11_06_IT_PTP_INV.ACDOCA_BLDAT, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) AS ZF_BLDAT_AGE_DIFF --Z_Age (document date)
		,B11_06_IT_PTP_INV.ACDOCA_ZFBDT --Baseline date
		,DATEDIFF(dd, B11_06_IT_PTP_INV.ACDOCA_ZFBDT, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) AS ZF_ZFBDT_AGE_DIFF --Z_Age (baseline date)
		,B11_06_IT_PTP_INV.ACDOCA_BUDAT--Posting date
		,DATEDIFF(dd, B11_06_IT_PTP_INV.ACDOCA_BUDAT, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) AS ZF_BUDAT_AGE_DIFF --Z_Age (posting date)
		,CASE 
			WHEN DATEDIFF(dd, B11_06_IT_PTP_INV.ZF_ACDOCA_ZBDXT_DUE_DATE, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) <= 0 THEN '1. Current (<= 0)' 
			WHEN DATEDIFF(dd, B11_06_IT_PTP_INV.ZF_ACDOCA_ZBDXT_DUE_DATE, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) BETWEEN 0  AND 30 THEN '2. 1 to 30' 
			WHEN DATEDIFF(dd, B11_06_IT_PTP_INV.ZF_ACDOCA_ZBDXT_DUE_DATE, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) BETWEEN 30 AND 60 THEN '3. 31 to 60' 
			WHEN DATEDIFF(dd, B11_06_IT_PTP_INV.ZF_ACDOCA_ZBDXT_DUE_DATE, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) BETWEEN 60 AND 90 THEN '4. 61 to 90' 
			WHEN DATEDIFF(dd, B11_06_IT_PTP_INV.ZF_ACDOCA_ZBDXT_DUE_DATE, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) BETWEEN 90 AND 180 THEN '5. 91 to 180' 
			WHEN DATEDIFF(dd, B11_06_IT_PTP_INV.ZF_ACDOCA_ZBDXT_DUE_DATE, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) BETWEEN 180 AND 360 THEN '6. 181 to 360'
			WHEN DATEDIFF(dd, B11_06_IT_PTP_INV.ZF_ACDOCA_ZBDXT_DUE_DATE, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) > 360 THEN '7. > 360'
--			ELSE '0. Unknown'
			ELSE 'No due date available'
		END ZF_ACDOCA_ZBDXT_AGE_BUCKET
	   ,CASE 
			WHEN DATEDIFF(dd, B11_06_IT_PTP_INV.ACDOCA_BLDAT, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) <= 0 THEN '1. Current (<= 0)' 
			WHEN DATEDIFF(dd, B11_06_IT_PTP_INV.ACDOCA_BLDAT, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) BETWEEN 0 AND 30 THEN '2. 1 to 30'
			WHEN DATEDIFF(dd, B11_06_IT_PTP_INV.ACDOCA_BLDAT, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) BETWEEN 30 AND 60 THEN '3. 31 to 60'
			WHEN DATEDIFF(dd, B11_06_IT_PTP_INV.ACDOCA_BLDAT, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) BETWEEN 60 AND 90 THEN '4. 61 to 90'
			WHEN DATEDIFF(dd, B11_06_IT_PTP_INV.ACDOCA_BLDAT, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) BETWEEN 90 AND 180 THEN '5. 91 to 180'
			WHEN DATEDIFF(dd, B11_06_IT_PTP_INV.ACDOCA_BLDAT, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) BETWEEN 180 AND 360 THEN '6. 181 to 360'
			WHEN DATEDIFF(dd, B11_06_IT_PTP_INV.ACDOCA_BLDAT, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) > 360 THEN '7. > 360'
--			ELSE '0. Unknown'
			ELSE 'No document date available'
		END ZF_BLDAT_AGE_BUCKET
	   ,CASE 
			WHEN DATEDIFF(dd, B11_06_IT_PTP_INV.ACDOCA_ZFBDT, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) <= 0 THEN '1. Current (<= 0)' 
			WHEN DATEDIFF(dd, B11_06_IT_PTP_INV.ACDOCA_ZFBDT, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) BETWEEN 0 AND 30 THEN '2. 1 to 30' 
			WHEN DATEDIFF(dd, B11_06_IT_PTP_INV.ACDOCA_ZFBDT, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) BETWEEN 30 AND 60 THEN '3. 31 to 60' 
			WHEN DATEDIFF(dd, B11_06_IT_PTP_INV.ACDOCA_ZFBDT, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) BETWEEN 60 AND 90 THEN '4. 61 to 90' 
			WHEN DATEDIFF(dd, B11_06_IT_PTP_INV.ACDOCA_ZFBDT, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) BETWEEN 90 AND 180 THEN '5. 91 to 180' 
			WHEN DATEDIFF(dd, B11_06_IT_PTP_INV.ACDOCA_ZFBDT, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) BETWEEN 180 AND 360 THEN '6. 181 to 360'
			WHEN DATEDIFF(dd, B11_06_IT_PTP_INV.ACDOCA_ZFBDT, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) > 360 THEN '7. > 360'
			ELSE 'No baseline date available'
		END ZF_ZFBDT_AGE_BUCKET
	   ,CASE 
			WHEN DATEDIFF(dd, B11_06_IT_PTP_INV.ACDOCA_BUDAT, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) <= 0 THEN '1. Current (<= 0)' 
			WHEN DATEDIFF(dd, B11_06_IT_PTP_INV.ACDOCA_BUDAT, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) BETWEEN 0 AND 30 THEN '2. 1 to 30' 
			WHEN DATEDIFF(dd, B11_06_IT_PTP_INV.ACDOCA_BUDAT, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) BETWEEN 30 AND 60 THEN '3. 31 to 60' 
			WHEN DATEDIFF(dd, B11_06_IT_PTP_INV.ACDOCA_BUDAT, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) BETWEEN 60 AND 90 THEN '4. 91 to 90' 
			WHEN DATEDIFF(dd, B11_06_IT_PTP_INV.ACDOCA_BUDAT, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) BETWEEN 90 AND 180 THEN '5. 91 to 180' 
			WHEN DATEDIFF(dd, B11_06_IT_PTP_INV.ACDOCA_BUDAT, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) BETWEEN 180 AND 360 THEN '6. 181 to 360'
			WHEN DATEDIFF(dd, B11_06_IT_PTP_INV.ACDOCA_BUDAT, B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END) > 360 THEN '7. > 360'
			ELSE 'No posting date available'
		END ZF_BUDAT_AGE_BUCKET
	INTO B11_14_IT_AP_AGEING
	FROM B11_06_IT_PTP_INV
	LEFT JOIN B11_12_TT_SNAPSHOT_BUKRS
	ON  B11_06_IT_PTP_INV.ACDOCA_RBUKRS = B11_12_TT_SNAPSHOT_BUKRS.T001_BUKRS
	WHERE (ISNULL(B11_06_IT_PTP_INV.ACDOCA_AUGDT, '') = '' AND B11_06_IT_PTP_INV.ACDOCA_BUDAT <= B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END )
	--EXTRA SCRIPT
	OR (B11_06_IT_PTP_INV.ACDOCA_AUGDT > B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END  AND B11_06_IT_PTP_INV.ACDOCA_BUDAT <= B11_12_TT_SNAPSHOT_BUKRS.ZF_MNTH_END )



/*Rename fields for Qlik*/
	  DELETE B11_05_IT_PTP_INVS_AND_CANCS
   WHERE (ACDOCA_GJAHR < YEAR(CAST(@DATE1 AS date)) OR ACDOCA_GJAHR > YEAR(CAST(@DATE2 AS date))) AND DB_NAME() NOT LIKE '%SPE%' 
	EXEC SP_RENAME_FIELD 'B11_', 'B11_05_IT_PTP_INVS_AND_CANCS'
	EXEC SP_RENAME_FIELD 'B11B_', 'B11_04_IT_PTP_APA'
	EXEC SP_RENAME_FIELD 'B11C_', 'B11_06_IT_PTP_INV'
	EXEC SP_RENAME_FIELD 'B11D_', 'B11_07_IT_PTP_INV_CANC'
	EXEC SP_RENAME_FIELD 'B11E_', 'B11_08_IT_PTP_PAY'
	EXEC SP_RENAME_FIELD 'B11F_', 'B11_09_IT_PTP_PAY_CANC'
	EXEC SP_RENAME_FIELD 'B11G_', 'B11_14_IT_AP_AGEING'

/*
	Thuan : 08/02/2021 Make payment date dashboard. Base on request from Jesper. */
EXEC SP_REMOVE_TABLES 'B11_15_IT_INV_PAY'
SELECT A.*, B.*
	   ,CASE 
			WHEN DATEDIFF(dd, A.B11C_ACDOCA_BUDAT, B.B11E_ACDOCA_BUDAT) <= 0 THEN '<= 0' 
			WHEN DATEDIFF(dd, A.B11C_ACDOCA_BUDAT, B.B11E_ACDOCA_BUDAT) BETWEEN 1 AND 33 THEN '1 - 33' 
			WHEN DATEDIFF(dd, A.B11C_ACDOCA_BUDAT, B.B11E_ACDOCA_BUDAT) BETWEEN 34 AND 60 THEN '34 - 60' 
			WHEN DATEDIFF(dd, A.B11C_ACDOCA_BUDAT, B.B11E_ACDOCA_BUDAT) BETWEEN 61 AND 90 THEN '61 - 90' 
			WHEN DATEDIFF(dd, A.B11C_ACDOCA_BUDAT, B.B11E_ACDOCA_BUDAT) BETWEEN 91 AND 120 THEN '91 - 120' 
			WHEN DATEDIFF(dd, A.B11C_ACDOCA_BUDAT, B.B11E_ACDOCA_BUDAT) BETWEEN 121 AND 180 THEN '121 - 180' 
			WHEN DATEDIFF(dd, A.B11C_ACDOCA_BUDAT, B.B11E_ACDOCA_BUDAT) BETWEEN 181 AND 360 THEN '181 - 360'
			ELSE '> 360'
		END ZF_PAY_MINUS_INV_BUDAT_BUCKET
		,CASE 
			WHEN DATEDIFF(dd, A.B11C_ACDOCA_BLDAT, B.B11E_ACDOCA_BLDAT) <= 0 THEN '<= 0' 
			WHEN DATEDIFF(dd, A.B11C_ACDOCA_BLDAT, B.B11E_ACDOCA_BLDAT) BETWEEN 1 AND 33 THEN '1 - 33' 
			WHEN DATEDIFF(dd, A.B11C_ACDOCA_BLDAT, B.B11E_ACDOCA_BLDAT) BETWEEN 34 AND 60 THEN '34 - 60' 
			WHEN DATEDIFF(dd, A.B11C_ACDOCA_BLDAT, B.B11E_ACDOCA_BLDAT) BETWEEN 61 AND 90 THEN '61 - 90' 
			WHEN DATEDIFF(dd, A.B11C_ACDOCA_BLDAT, B.B11E_ACDOCA_BLDAT) BETWEEN 91 AND 120 THEN '91 - 120' 
			WHEN DATEDIFF(dd, A.B11C_ACDOCA_BLDAT, B.B11E_ACDOCA_BLDAT) BETWEEN 121 AND 180 THEN '121 - 180' 
			WHEN DATEDIFF(dd, A.B11C_ACDOCA_BLDAT, B.B11E_ACDOCA_BLDAT) BETWEEN 181 AND 360 THEN '181 - 360'
			ELSE '> 360'
		END ZF_PAY_MINUS_INV_BLDAT_BUCKET
INTO B11_15_IT_INV_PAY
FROM B11_06_IT_PTP_INV AS A
INNER  JOIN B11_08_IT_PTP_PAY AS B
ON A.B11C_ACDOCA_RBUKRS = B.B11E_ACDOCA_RBUKRS
AND A.B11C_ACDOCA_AUGBL = B.B11E_ACDOCA_AUGBL
AND A.B11C_ACDOCA_AUGGJ = B.B11E_ACDOCA_AUGGJ
WHERE A.B11C_ACDOCA_AUGBL <> '' AND B.B11E_ACDOCA_AUGBL <> '' 


/*Remove temporary tables*/

EXEC SP_REMOVE_TABLES '%_TT_%'

/* log cube creation*/

INSERT INTO [DBO].[LOG_SP_EXECUTION] ([DATABASE],[OBJECT],[OBJECT_TYPE],[USER],[DATE],[TIME],[DESCRIPTION],[TABLE],[ROWS])
SELECT DB_NAME(),OBJECT_NAME(@@PROCID),'P',SYSTEM_USER,CONVERT(DATE,GETDATE()),CONVERT(TIME,GETDATE()),'Cube completed','B11_05_IT_PTP_INVS_AND_CANCS',(SELECT COUNT(*) FROM B11_05_IT_PTP_INVS_AND_CANCS) 
INSERT INTO [DBO].[LOG_SP_EXECUTION] ([DATABASE],[OBJECT],[OBJECT_TYPE],[USER],[DATE],[TIME],[DESCRIPTION],[TABLE],[ROWS])
SELECT DB_NAME(),OBJECT_NAME(@@PROCID),'P',SYSTEM_USER,CONVERT(DATE,GETDATE()),CONVERT(TIME,GETDATE()),'Cube completed','B11_04_IT_PTP_APA',(SELECT COUNT(*) FROM B11_04_IT_PTP_APA) 
INSERT INTO [DBO].[LOG_SP_EXECUTION] ([DATABASE],[OBJECT],[OBJECT_TYPE],[USER],[DATE],[TIME],[DESCRIPTION],[TABLE],[ROWS])
SELECT DB_NAME(),OBJECT_NAME(@@PROCID),'P',SYSTEM_USER,CONVERT(DATE,GETDATE()),CONVERT(TIME,GETDATE()),'Cube completed','B11_06_IT_PTP_INV',(SELECT COUNT(*) FROM B11_06_IT_PTP_INV) 
INSERT INTO [DBO].[LOG_SP_EXECUTION] ([DATABASE],[OBJECT],[OBJECT_TYPE],[USER],[DATE],[TIME],[DESCRIPTION],[TABLE],[ROWS])
SELECT DB_NAME(),OBJECT_NAME(@@PROCID),'P',SYSTEM_USER,CONVERT(DATE,GETDATE()),CONVERT(TIME,GETDATE()),'Cube completed','B11_07_IT_PTP_INV_CANC',(SELECT COUNT(*) FROM B11_07_IT_PTP_INV_CANC) 
INSERT INTO [DBO].[LOG_SP_EXECUTION] ([DATABASE],[OBJECT],[OBJECT_TYPE],[USER],[DATE],[TIME],[DESCRIPTION],[TABLE],[ROWS])
SELECT DB_NAME(),OBJECT_NAME(@@PROCID),'P',SYSTEM_USER,CONVERT(DATE,GETDATE()),CONVERT(TIME,GETDATE()),'Cube completed','B11_08_IT_PTP_PAY',(SELECT COUNT(*) FROM B11_08_IT_PTP_PAY) 
INSERT INTO [DBO].[LOG_SP_EXECUTION] ([DATABASE],[OBJECT],[OBJECT_TYPE],[USER],[DATE],[TIME],[DESCRIPTION],[TABLE],[ROWS])
SELECT DB_NAME(),OBJECT_NAME(@@PROCID),'P',SYSTEM_USER,CONVERT(DATE,GETDATE()),CONVERT(TIME,GETDATE()),'Cube completed','B11_09_IT_PTP_PAY_CANC',(SELECT COUNT(*) FROM B11_09_IT_PTP_PAY_CANC) 


/* log end of procedure*/
INSERT INTO [DBO].[LOG_SP_EXECUTION] ([DATABASE],[OBJECT],[OBJECT_TYPE],[USER],[DATE],[TIME],[DESCRIPTION],[TABLE],[ROWS])
SELECT DB_NAME(),OBJECT_NAME(@@PROCID),'P',SYSTEM_USER,CONVERT(date,GETDATE()),CONVERT(time,GETDATE()),'Procedure finished',NULL,NULL
GO
