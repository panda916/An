USE [DIVA_MASTER_SCRIPT_S4HANA]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE [dbo].[script_DC13_PTP_GLOBAL_CUBES]
AS

--DYNAMIC_SCRIPT_START
/* Initiate the log */ 
----Create database log table if it does not exist
IF OBJECT_ID('LOG_SP_EXECUTION', 'U') IS NULL BEGIN CREATE TABLE [DBO].[LOG_SP_EXECUTION] ([DATABASE] NVARCHAR(MAX) NULL,[OBJECT] NVARCHAR(MAX) NULL,[OBJECT_TYPE] NVARCHAR(MAX) NULL,[USER] NVARCHAR(MAX) NULL,[DATE] DATE NULL,[TIME] TIME NULL,[DESCRIPTION] NVARCHAR(MAX) NULL,[TABLE] NVARCHAR(MAX),[ROWS] INT) END
 
--Log start of procedure
INSERT INTO [DBO].[LOG_SP_EXECUTION] ([DATABASE],[OBJECT],[OBJECT_TYPE],[USER],[DATE],[TIME],[DESCRIPTION],[TABLE],[ROWS])
SELECT DB_NAME(),OBJECT_NAME(@@PROCID),'P',SYSTEM_USER,CONVERT(DATE,GETDATE()),CONVERT(TIME,GETDATE()),'Procedure started',NULL,NULL
 
/* Initialize parameters from globals table */


DECLARE  
                      @CURRENCY NVARCHAR(MAX)                 = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'currency')
                     ,@DATE1 NVARCHAR(MAX)                           = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'date1')
                     ,@DATE2 NVARCHAR(MAX)                           = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'date2')
                     ,@DOWNLOADDATE NVARCHAR(MAX)             = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'downloaddate')
                     ,@DATEFORMAT VARCHAR(3)             = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'dateformat')
                     ,@EXCHANGERATETYPE NVARCHAR(MAX)  = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'exchangeratetype')
                     ,@LANGUAGE1 NVARCHAR(MAX)                = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'language1')
                     ,@LANGUAGE2 NVARCHAR(MAX)                = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'language2')
                     ,@YEAR NVARCHAR(MAX)                     = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'year')
                     ,@ID NVARCHAR(MAX)                       = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'id')
                     ,@LIMIT_RECORDS INT                    = CAST((SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'LIMIT_RECORDS') AS INT)
					 ,@ZV_SAME_QUARTER_BY_BLDAT NVARCHAR(MAX) = ISNULL((SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'ZV_SAME_QUARTER_BY_BLDAT'), '')


EXEC SP_DROPTABLE 'DC13_01_IT_PTP_GLOBAL_TABLE'

SELECT     A.B27_ZF_DOCUMENT_FLOW_KEY,
		   A.B27_ZF_RSEG_BELNR_GJAHR_BUZEI, --Incoming invoice key
		   MAX(A_RSEG.RSEG_BELNR) AS RSEG_BELNR,
		   MAX(A_RSEG.RSEG_GJAHR) AS RSEG_GJAHR,
		   MAX(A_RSEG.RSEG_BUZEI) AS RSEG_BUZEI,
		   MAX(A_RBKP.RBKP_WAERS) AS RBKP_WAERS,
		   MAX(A_T001.T001_WAERS) AS T001_WAERS,
		   MAX(A_RBKP.RBKP_BUDAT) AS RBKP_BUDAT,
		   MAX(AM_EXCHNG.EXCHNG_RATIO) AS ZF_EXCHNG_RATIO_OLD,
		   MAX(RBKP_KURSF * COALESCE(TCURF_COC.TCURF_TFACT,1)/COALESCE(TCURF_COC.TCURF_FFACT,1) * COALESCE(CAST(TCURR_CUC.TCURR_UKURS AS FLOAT),1) * COALESCE(TCURF_CUC.TCURF_TFACT,1) / COALESCE(TCURF_CUC.TCURF_FFACT,1)) AS ZF_EXCHNG_RATIO_NEW,
		   SUM(A.B27_ZF_RSEG_WRBTR_S) AS B27_ZF_RSEG_WRBTR_S,
		   SUM(B.B27_ZF_RSEG_WRBTR_S_CUC) AS B27_ZF_RSEG_WRBTR_S_CUC_OLD,
		   SUM(A.B27_ZF_RSEG_WRBTR_S_CUC) AS B27_ZF_RSEG_WRBTR_S_CUC_NEW
INTO DC13_01_IT_PTP_GLOBAL_TABLE
FROM
(
	SELECT B27_ZF_DOCUMENT_FLOW_KEY,
		   B27_ZF_RSEG_BELNR_GJAHR_BUZEI, --Incoming invoice 
		   REPLACE(SUBSTRING(B27_ZF_RSEG_BELNR_GJAHR_BUZEI, 1, CHARINDEX('-', B27_ZF_RSEG_BELNR_GJAHR_BUZEI)),'-','') AS B27_ZF_RSEG_BELNR,
		   SUM(B27_ZF_RSEG_WRBTR_S) AS B27_ZF_RSEG_WRBTR_S,
		   SUM(B27_ZF_RSEG_WRBTR_S_CUC) AS B27_ZF_RSEG_WRBTR_S_CUC
	FROM B27_01_IT_PTP_GLOBAL_TABLE_NEW
--	WHERE LEN(B27_ZF_RSEG_BELNR_GJAHR_BUZEI) > 2
	GROUP BY 
		   B27_ZF_DOCUMENT_FLOW_KEY,
		   B27_ZF_RSEG_BELNR_GJAHR_BUZEI --Incoming invoice key
) AS A
INNER JOIN
(
	SELECT B27_ZF_DOCUMENT_FLOW_KEY,
		   B27_ZF_RSEG_BELNR_GJAHR_BUZEI, --Incoming invoice key
		   REPLACE(SUBSTRING(B27_ZF_RSEG_BELNR_GJAHR_BUZEI, 1, CHARINDEX('-', B27_ZF_RSEG_BELNR_GJAHR_BUZEI)),'-','') AS B27_ZF_RSEG_BELNR,
		   SUM(B27_ZF_RSEG_WRBTR_S) AS B27_ZF_RSEG_WRBTR_S,
		   SUM(B27_ZF_RSEG_WRBTR_S_CUC) AS B27_ZF_RSEG_WRBTR_S_CUC
	FROM B27_01_IT_PTP_GLOBAL_TABLE
--	WHERE LEN(B27_ZF_RSEG_BELNR_GJAHR_BUZEI) > 2
	GROUP BY 
		   B27_ZF_DOCUMENT_FLOW_KEY,
		   B27_ZF_RSEG_BELNR_GJAHR_BUZEI --Incoming invoice key
) AS B
ON A.B27_ZF_DOCUMENT_FLOW_KEY = B.B27_ZF_DOCUMENT_FLOW_KEY
	AND A.B27_ZF_RSEG_BELNR_GJAHR_BUZEI = B.B27_ZF_RSEG_BELNR_GJAHR_BUZEI

-- Join to A_RBKP to add RBKP_BUDAT and RBKP_WAERS

LEFT JOIN A_RSEG
	ON A_RSEG.RSEG_BELNR = A.B27_ZF_RSEG_BELNR

LEFT JOIN A_RBKP ON RBKP_BELNR=RSEG_BELNR AND RBKP_GJAHR=RSEG_GJAHR

-- Add currency factors

LEFT JOIN B00_TCURX AS TCURX_DOC  
	ON RBKP_WAERS = TCURX_DOC.TCURX_CURRKEY

-- Add company code

LEFT JOIN A_T001 
ON RBKP_BUKRS = A_T001.T001_BUKRS 

-- Add manual exchange rates

LEFT JOIN AM_EXCHNG 
	ON RBKP_WAERS = AM_EXCHNG.EXCHNG_FROM

-- Add currency factor from company currency to USD

LEFT JOIN B00_IT_TCURF TCURF_CUC
ON A_T001.T001_WAERS = TCURF_CUC.TCURF_FCURR
AND TCURF_CUC.TCURF_TCURR  = @currency  
AND TCURF_CUC.TCURF_GDATU = (
	SELECT TOP 1 B00_IT_TCURF.TCURF_GDATU
	FROM B00_IT_TCURF
	WHERE A_T001.T001_WAERS = B00_IT_TCURF.TCURF_FCURR AND 
			B00_IT_TCURF.TCURF_TCURR  = @currency  AND
			B00_IT_TCURF.TCURF_GDATU <= RBKP_BUDAT
	ORDER BY B00_IT_TCURF.TCURF_GDATU DESC
	)

-- Add exchange rate from company currency to USD

LEFT JOIN B00_IT_TCURR TCURR_CUC
	ON A_T001.T001_WAERS = TCURR_CUC.TCURR_FCURR
	AND TCURR_CUC.TCURR_TCURR  = @currency  
	AND TCURR_CUC.TCURR_GDATU = (
		SELECT TOP 1 B00_IT_TCURR.TCURR_GDATU
		FROM B00_IT_TCURR
		WHERE A_T001.T001_WAERS = B00_IT_TCURR.TCURR_FCURR AND 
				B00_IT_TCURR.TCURR_TCURR  = @currency  AND
				B00_IT_TCURR.TCURR_GDATU <= RBKP_BUDAT
		ORDER BY B00_IT_TCURR.TCURR_GDATU DESC
		) 

-- Add currency factor from document currency to local currency

LEFT JOIN B00_IT_TCURF TCURF_COC
ON RBKP_WAERS = TCURF_COC.TCURF_FCURR
AND TCURF_COC.TCURF_TCURR  = T001_WAERS  
AND TCURF_COC.TCURF_GDATU = (
	SELECT TOP 1 B00_IT_TCURF.TCURF_GDATU
	FROM B00_IT_TCURF
	WHERE RBKP_WAERS = B00_IT_TCURF.TCURF_FCURR AND 
			B00_IT_TCURF.TCURF_TCURR  = T001_WAERS  AND
			B00_IT_TCURF.TCURF_GDATU <= RBKP_BUDAT
	ORDER BY B00_IT_TCURF.TCURF_GDATU DESC
	)
GROUP BY A.B27_ZF_DOCUMENT_FLOW_KEY,
		 A.B27_ZF_RSEG_BELNR_GJAHR_BUZEI

EXEC SP_UNNAME_FIELD 'B27_',DC13_01_IT_PTP_GLOBAL_TABLE
EXEC SP_RENAME_FIELD 'DC13_01_',DC13_01_IT_PTP_GLOBAL_TABLE

GO
