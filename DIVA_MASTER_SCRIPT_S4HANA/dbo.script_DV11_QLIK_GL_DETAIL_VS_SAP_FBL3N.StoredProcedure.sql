USE [DIVA_MASTER_SCRIPT_S4HANA]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

CREATE     PROC [dbo].[script_DV11_QLIK_GL_DETAIL_VS_SAP_FBL3N]
WITH EXECUTE AS CALLER
AS
--DYNAMIC_SCRIPT_START
BEGIN
 

/* Initiate the log */  
--Create database log table if it does not exist
IF OBJECT_ID('LOG_SP_EXECUTION', 'U') IS NULL BEGIN CREATE TABLE [DBO].[LOG_SP_EXECUTION] ([DATABASE] NVARCHAR(MAX) NULL,[OBJECT] NVARCHAR(MAX) NULL,[OBJECT_TYPE] NVARCHAR(MAX) NULL,[USER] NVARCHAR(MAX) NULL,[DATE] DATE NULL,[TIME] TIME NULL,[DESCRIPTION] NVARCHAR(MAX) NULL,[TABLE] NVARCHAR(MAX),[ROWS] INT) END

--Log start of procedure
INSERT INTO [DBO].[LOG_SP_EXECUTION] ([DATABASE],[OBJECT],[OBJECT_TYPE],[USER],[DATE],[TIME],[DESCRIPTION],[TABLE],[ROWS])
SELECT DB_NAME(),OBJECT_NAME(@@PROCID),'P',SYSTEM_USER,CONVERT(DATE,GETDATE()),CONVERT(TIME,GETDATE()),'Procedure started',NULL,NULL


/* Initialize parameters from globals table */

     DECLARE 	 
			 @CURRENCY NVARCHAR(MAX)			= (SELECT GLOBALS_VALUE FROM AM_GLOBALS WHERE GLOBALS_PARAMETER = 'currency')
			,@DATE1 NVARCHAR(MAX)				= (SELECT GLOBALS_VALUE FROM AM_GLOBALS WHERE GLOBALS_PARAMETER = 'date1')
			,@DATE2 NVARCHAR(MAX)				= (SELECT GLOBALS_VALUE FROM AM_GLOBALS WHERE GLOBALS_PARAMETER = 'date2')
			,@DOWNLOADDATE NVARCHAR(MAX)		= (SELECT GLOBALS_VALUE FROM AM_GLOBALS WHERE GLOBALS_PARAMETER = 'downloaddate')
			,@EXCHANGERATETYPE NVARCHAR(MAX)	= (SELECT GLOBALS_VALUE FROM AM_GLOBALS WHERE GLOBALS_PARAMETER = 'exchangeratetype')
			,@LANGUAGE1 NVARCHAR(MAX)			= (SELECT GLOBALS_VALUE FROM AM_GLOBALS WHERE GLOBALS_PARAMETER = 'language1')
			,@LANGUAGE2 NVARCHAR(MAX)			= (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'language2')
			,@YEAR NVARCHAR(MAX)				= (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'year')
			,@ID NVARCHAR(MAX)					= (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'id')
			,@ZV_LIMIT NVARCHAR(MAX)		    = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'ZV_LIMIT')
			

DECLARE @DATEFORMAT VARCHAR(3)
SET @DATEFORMAT   = (SELECT DBO.GET_PARAM('dateformat'))
SET DATEFORMAT @DATEFORMAT;



/*
    Title            :  DV11: Data Validation dashboard 
    Description      :  Compares the RTR GL Detail report with SAP  report table
       --------------------------------------------------------------
    Update history
    --------------------------------------------------------------
    Date             |     Who		|  Description
    22-08-2017             Anh         Creation
	24-10-2018             Hung        Naming convention and logic fixes
*/


-- Step 1/ Set up the code that will enable message to be sent back to the user of the Java app
	-- that decides to run the script
	--CREATE log table TO RETURN RESULTS FOR USERS
	-- Variables for user message for feedback
	-- First two columns are used for SP_QlikNamingClassifier,
	-- Third column is only one used for sending messages to user for progress of DV scripts
	EXEC SP_REMOVE_TABLES 'DV11_%'

	DECLARE @MSG NVARCHAR(MAX)
	EXEC SP_REMOVE_TABLES 'DV00_RT_USER_FEEDBACK_MESSAGE'
	CREATE TABLE DV00_RT_USER_FEEDBACK_MESSAGE         
		 (OLD_NAME NVARCHAR(MAX), NEW_NAME NVARCHAR(MAX), _LOG NVARCHAR(MAX))

	-- The following code can be put wherever we want and whenever we want to send a message back to the user
	-- that has pressed Java Upload app button for running this script.

	--SET @MSG = '<Feedback message for user>' --print back status for users
	--INSERT INTO DV00_RT_USER_FEEDBACK_MESSAGE VALUES('', '', @MSG)

-- Step 2/
-- Load data from Qlik Report 
-- Calculate the total value per FY, Company, and GL account
-- At the same time, renaming with table name as prefix

	
	EXECUTE SP_REMOVE_TABLES 'DV11_01_TT_AQLIK_RTR_GL_DET'
	SELECT	
			LEFT([Company code],4) AQLIK_RBUKRS,
			[Fisical year]  AQLIK_GJAHR,
			LEFT([Document Type],2) AQLIK_BLART,
			LEFT([GL account],7) AQLIK_RACCT,  
			[Debit/Credit] AQLIK_DRCRK,
			DBO.REMOVE_LEADING_ZEROES([Document nr]) AQLIK_BELNR,  
			SUM(DBO.STRING_TO_NUMBER(REPLACE([Value (COC)],',',''))) AQLIK_HSL
	INTO DV11_01_TT_AQLIK_RTR_GL_DET
	FROM AQLIK_RTR_GL_DETAIL
	GROUP BY [Company code], [Fisical year], [Document Type], [Document nr], [GL account], [Debit/Credit]

-- Step 3/
-- Load data from SAP report
-- At the same time renaming with the SAP report table prefix
-- Calculate the total value per GL account for the SAP report
-- At the same time renaming with the SAP report table prefix

	EXECUTE SP_REMOVE_TABLES 'DV11_02_TT_SAP_RTR_GL_DET'
	SELECT BLART SAP_BLART,
		DBO.REMOVE_LEADING_ZEROES(ACCT_FROM_FILE_NAME) SAP_RACCT,
		DBO.REMOVE_LEADING_ZEROES(BELNR) SAP_BELNR,
		BUKRS_FROM_FILE_NAME SAP_RBUKRS,
		FY_FROM_FILE_NAME as SAP_GJAHR,
		SUM(HSL_CONVERTED) SAP_HSL,
		IIF(HSL_CONVERTED < 0, 'Credit', 'Debit') ZF_SAP_DRCRK
	INTO DV11_02_TT_SAP_RTR_GL_DET
	FROM ASAP_FBL3N
	GROUP BY BUKRS_FROM_FILE_NAME, FY_FROM_FILE_NAME, BLART, DBO.REMOVE_LEADING_ZEROES(ACCT_FROM_FILE_NAME), BELNR, IIF(HSL_CONVERTED < 0, 'Credit', 'Debit')

--Step 4/Company Code from File Name
--Merge Data from Qlik and SAP report
--Calculate missing gl or business area or document type or document number from SAP and QLIK

	EXECUTE SP_REMOVE_TABLES 'DV11_03_RT_COMP_AQLIK_RTR_SAPRTR'
	SELECT	
			AQLIK_RBUKRS,
			AQLIK_GJAHR,
			AQLIK_BLART,
			AQLIK_RACCT,   
			AQLIK_BELNR,  
			AQLIK_DRCRK,
			ISNULL(AQLIK_HSL,0) AS AQLIK_HSL,
			
			SAP_BLART,
			SAP_RACCT,
			SAP_BELNR,
			SAP_RBUKRS,
			SAP_GJAHR,
			ZF_SAP_DRCRK,
			
			ISNULL(SAP_HSL, 0) SAP_HSL,
			ISNULL (AQLIK_HSL, 0)	- ISNULL(SAP_HSL, 0) ZF_AQLIK_MINUS_SAP_HSL,
			CASE WHEN	ISNULL(AQLIK_BELNR ,'')	= '' AND
						ISNULL(SAP_BELNR ,'')	<> '' 
					THEN 'Document in SAP not DIVA'
				 WHEN	ISNULL(AQLIK_BELNR ,'')	<> '' AND
						ISNULL(SAP_BELNR,'') = '' 
					THEN 'Document in DIVA not SAP'
				WHEN	ISNULL(AQLIK_BELNR, '')	<> '' AND
						ISNULL(SAP_BELNR, '')	<> ''
					THEN 'Document not missing'			
				ELSE	'Case not identified'
			END ZF_BELNR_MISSING					
	INTO DV11_03_RT_COMP_AQLIK_RTR_SAPRTR
	FROM DV11_01_TT_AQLIK_RTR_GL_DET
	FULL OUTER JOIN DV11_02_TT_SAP_RTR_GL_DET
		ON AQLIK_RBUKRS = SAP_RBUKRS
		AND AQLIK_GJAHR = SAP_GJAHR
		AND AQLIK_BELNR = SAP_BELNR
		AND AQLIK_BLART = SAP_BLART
		AND AQLIK_RACCT = SAP_RACCT
		AND AQLIK_DRCRK = ZF_SAP_DRCRK
--Step 5/
--Split the comparison into two tables so that we can be able to filter them separately

	EXEC SP_REMOVE_TABLES 'DV11_04_RT_FULL_AQLIK_RTR_DET'
	SELECT	
			AQLIK_RBUKRS, 
			AQLIK_GJAHR,
			AQLIK_BLART,
			AQLIK_RACCT,   
			AQLIK_BELNR,  
			AQLIK_HSL,
			AQLIK_DRCRK,
			AQLIK_RBUKRS + '|' + AQLIK_RACCT + '|' + AQLIK_GJAHR + '|' + AQLIK_BELNR ZF_KEY_JOIN_QLIK
	INTO	DV11_04_RT_FULL_AQLIK_RTR_DET
	FROM	DV11_03_RT_COMP_AQLIK_RTR_SAPRTR
	WHERE	ISNULL(AQLIK_RACCT, '') <> ''
	
	EXEC SP_REMOVE_TABLES 'DV11_05_RT_FULL_SAP_RTR_DET'
	SELECT	
			SAP_RBUKRS,
			SAP_GJAHR,
			SAP_BLART,
			SAP_RACCT,
			SAP_BELNR, 
			SAP_HSL,
			ZF_SAP_DRCRK,
			SAP_RBUKRS  + '|' + SAP_RACCT + '|' + SAP_GJAHR + '|' + SAP_BELNR ZF_KEY_JOIN_SAP
	INTO	DV11_05_RT_FULL_SAP_RTR_DET
	FROM	DV11_03_RT_COMP_AQLIK_RTR_SAPRTR
	WHERE	ISNULL(SAP_RACCT, '') <> ''
	
--Step 6/
--Split the comparison into two discrepancy tables so that we can be able to filter them separately
--06/06/2019 by Thuan ADD more code "AND NOT	(ABS(ZF_AQLIK_MINUS_SAP_HSL)>10 AND 
--		ZF_BELNR_MISSING = 'Document in SAP not DIVA')"

	EXEC SP_REMOVE_TABLES 'DV11_06_RT_DISC_AQLIK_RTR'
	SELECT	AQLIK_RBUKRS,
			AQLIK_GJAHR,
			AQLIK_BLART,
			AQLIK_RACCT,
			AQLIK_DRCRK,   
			AQLIK_BELNR,  
			AQLIK_HSL,
			ZF_AQLIK_MINUS_SAP_HSL,

			ZF_BELNR_MISSING,
			
			AQLIK_RBUKRS  + '|' +  AQLIK_RACCT + '|' + AQLIK_GJAHR + '|' + AQLIK_BELNR AS ZF_KEY_JOIN_QLIK	
	INTO	DV11_06_RT_DISC_AQLIK_RTR
	FROM	DV11_03_RT_COMP_AQLIK_RTR_SAPRTR
	WHERE	(ABS(ZF_AQLIK_MINUS_SAP_HSL) > 10  AND  ZF_BELNR_MISSING IN ('Document not missing'))
			OR	ZF_BELNR_MISSING IN ('Document in DIVA not SAP')


	EXEC SP_REMOVE_TABLES 'DV11_07_RT_DISC_SAP_RTR_DET'
	SELECT	
	
			SAP_RBUKRS,
			SAP_GJAHR,
			SAP_BLART,
			SAP_RACCT,
			ZF_SAP_DRCRK,
			SAP_BELNR, 
			SAP_HSL,
			ZF_AQLIK_MINUS_SAP_HSL,
			
			ZF_BELNR_MISSING,
			
			SAP_RBUKRS + '|' + SAP_RACCT + '|' + SAP_GJAHR + '|' + SAP_BELNR	AS ZF_KEY_JOIN_SAP
	INTO	DV11_07_RT_DISC_SAP_RTR_DET
	FROM	DV11_03_RT_COMP_AQLIK_RTR_SAPRTR
	WHERE	ZF_BELNR_MISSING IN ('Document in SAP not DIVA', 'Case not identified')
			OR (ABS(ZF_AQLIK_MINUS_SAP_HSL) > 10 AND ZF_BELNR_MISSING IN ('Document not missing'))

--Step 7/
--Rename field
EXEC SP_RENAME_FIELD 'DV11_', 'DV11_03_RT_COMP_AQLIK_RTR_SAPRTR'
EXEC SP_RENAME_FIELD 'DV11A_', 'DV11_04_RT_FULL_AQLIK_RTR_DET'
EXEC SP_RENAME_FIELD 'DV11B_', 'DV11_05_RT_FULL_SAP_RTR_DET'
EXEC SP_RENAME_FIELD 'DV11C_', 'DV11_06_RT_DISC_AQLIK_RTR'
EXEC SP_RENAME_FIELD 'DV11D_', 'DV11_07_RT_DISC_SAP_RTR_DET'

--Delete temporary tables
EXEC SP_REMOVE_TABLES '%_TT_%'


--Send Finish Message for the tool

	INSERT INTO DV00_RT_USER_FEEDBACK_MESSAGE VALUES('', '', 'DV11_COMP_QLIK_GL_DE_SAP_GL has finished')

/* log cube creation*/
INSERT INTO [DBO].[LOG_SP_EXECUTION] ([DATABASE],[OBJECT],[OBJECT_TYPE],[USER],[DATE],[TIME],[DESCRIPTION],[TABLE],[ROWS])
SELECT DB_NAME(),OBJECT_NAME(@@PROCID),'P',SYSTEM_USER,CONVERT(DATE,GETDATE()),CONVERT(TIME,GETDATE()),'Cube completed','DV11_03_RT_COMP_AQLIK_RTR_SAPRTR',(SELECT COUNT(*) FROM DV11_03_RT_COMP_AQLIK_RTR_SAPRTR) 
INSERT INTO [DBO].[LOG_SP_EXECUTION] ([DATABASE],[OBJECT],[OBJECT_TYPE],[USER],[DATE],[TIME],[DESCRIPTION],[TABLE],[ROWS])
SELECT DB_NAME(),OBJECT_NAME(@@PROCID),'P',SYSTEM_USER,CONVERT(DATE,GETDATE()),CONVERT(TIME,GETDATE()),'Cube completed','DV11_04_RT_FULL_AQLIK_RTR_DET',(SELECT COUNT(*) FROM DV11_04_RT_FULL_AQLIK_RTR_DET) 
INSERT INTO [DBO].[LOG_SP_EXECUTION] ([DATABASE],[OBJECT],[OBJECT_TYPE],[USER],[DATE],[TIME],[DESCRIPTION],[TABLE],[ROWS])
SELECT DB_NAME(),OBJECT_NAME(@@PROCID),'P',SYSTEM_USER,CONVERT(DATE,GETDATE()),CONVERT(TIME,GETDATE()),'Cube completed','DV11_05_RT_FULL_SAP_RTR_DET',(SELECT COUNT(*) FROM DV11_05_RT_FULL_SAP_RTR_DET) 
INSERT INTO [DBO].[LOG_SP_EXECUTION] ([DATABASE],[OBJECT],[OBJECT_TYPE],[USER],[DATE],[TIME],[DESCRIPTION],[TABLE],[ROWS])
SELECT DB_NAME(),OBJECT_NAME(@@PROCID),'P',SYSTEM_USER,CONVERT(DATE,GETDATE()),CONVERT(TIME,GETDATE()),'Cube completed','DV11_06_RT_DISC_AQLIK_RTR',(SELECT COUNT(*) FROM DV11_06_RT_DISC_AQLIK_RTR) 
INSERT INTO [DBO].[LOG_SP_EXECUTION] ([DATABASE],[OBJECT],[OBJECT_TYPE],[USER],[DATE],[TIME],[DESCRIPTION],[TABLE],[ROWS])
SELECT DB_NAME(),OBJECT_NAME(@@PROCID),'P',SYSTEM_USER,CONVERT(DATE,GETDATE()),CONVERT(TIME,GETDATE()),'Cube completed','DV11_07_RT_DISC_SAP_RTR_DET',(SELECT COUNT(*) FROM DV11_07_RT_DISC_SAP_RTR_DET) 

/* log end of procedure*/


INSERT INTO [DBO].[LOG_SP_EXECUTION] ([DATABASE],[OBJECT],[OBJECT_TYPE],[USER],[DATE],[TIME],[DESCRIPTION],[TABLE],[ROWS])
SELECT DB_NAME(),OBJECT_NAME(@@PROCID),'P',SYSTEM_USER,CONVERT(DATE,GETDATE()),CONVERT(TIME,GETDATE()),'Procedure finished',NULL,NULL

END
GO
