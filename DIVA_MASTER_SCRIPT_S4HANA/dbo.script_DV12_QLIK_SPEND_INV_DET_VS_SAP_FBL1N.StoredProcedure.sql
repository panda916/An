USE [DIVA_MASTER_SCRIPT_S4HANA]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

 

--ALTER PROCEDURE [dbo].[DV12_DIVA_SPEND_INV_DET_VS_SAP_FBL1N]
CREATE     PROCEDURE [dbo].[script_DV12_QLIK_SPEND_INV_DET_VS_SAP_FBL1N]
WITH EXECUTE AS CALLER
AS
--DYNAMIC_SCRIPT_START
BEGIN



/* Initiate the log */  
--Create database log table if it does not exist
IF OBJECT_ID('LOG_SP_EXECUTION', 'U') IS NULL BEGIN CREATE TABLE [DBO].[LOG_SP_EXECUTION] ([DATABASE] NVARCHAR(MAX) NULL,[OBJECT] NVARCHAR(MAX) NULL,[OBJECT_TYPE] NVARCHAR(MAX) NULL,[USER] NVARCHAR(MAX) NULL,[DATE] DATE NULL,[TIME] TIME NULL,[DESCRIPTION] NVARCHAR(MAX) NULL,[TABLE] NVARCHAR(MAX),[ROWS] INT) END

--Log start of procedure
INSERT INTO [DBO].[LOG_SP_EXECUTION] ([DATABASE],[OBJECT],[OBJECT_TYPE],[USER],[DATE],[TIME],[DESCRIPTION],[TABLE],[ROWS])
SELECT DB_NAME(),OBJECT_NAME(@@PROCID),'P',SYSTEM_USER,CONVERT(DATE,GETDATE()),CONVERT(TIME,GETDATE()),'Procedure started',NULL,NULL

/* Initialize parameters from globals table */

     DECLARE 	 
			 @CURRENCY NVARCHAR(MAX)			= (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'currency')
			,@DATE1 NVARCHAR(MAX)				= (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'date1')
			,@DATE2 NVARCHAR(MAX)				= (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'date2')
			,@DOWNLOADDATE NVARCHAR(MAX)		= (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'downloaddate')
			,@DATEFORMAT VARCHAR(3)             = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'dateformat')
			,@EXCHANGERATETYPE NVARCHAR(MAX)	= (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'exchangeratetype')
			,@LANGUAGE1 NVARCHAR(MAX)			= (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'language1')
			,@LANGUAGE2 NVARCHAR(MAX)			= (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'language2')
			,@YEAR NVARCHAR(MAX)				= (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'year')
			,@ID NVARCHAR(MAX)					= (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'id')
			,@LIMIT_RECORDS INT		            = CAST((SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'LIMIT_RECORDS') AS INT)
			

/*Test mode*/

SET ROWCOUNT @LIMIT_RECORDS

/*
    Title            : DV12: Compare DIVA Spend overview Summary of spend by supplier
	                   to SAP FBL1N
    --------------------------------------------------------------
    Update history
    --------------------------------------------------------------
    Date             |      Who |  Description
    08-08-2017             Nhat        Creation
	24-10-2018             Hung        Naming convention and logic fixes

	
*/


--Step 1/
-- Create table for send message back to DIVA_BULK_IMPORT.jar

	EXEC SP_REMOVE_TABLES 'DV00_RT_USER_FEEDBACK_MESSAGE'

	CREATE TABLE DV00_RT_USER_FEEDBACK_MESSAGE (OLD_NAME NVARCHAR(MAX), NEW_NAME NVARCHAR(MAX), _LOG NVARCHAR(MAX))

-- Step 2/

--AQLIK table need to create by  script from B11_04_PTP

	EXECUTE SP_REMOVE_TABLES 'DV12_01_TT_AQLIK_AP'
		SELECT 
		   B11B_ACDOCA_RBUKRS AQLIK_RBUKRS		
		  ,DBO.REMOVE_LEADING_ZEROES(B11B_ACDOCA_BELNR) AQLIK_BELNR
		  ,B11B_ACDOCA_GJAHR AQLIK_GJAHR
		  ,DBO.REMOVE_LEADING_ZEROES(B11B_ACDOCA_LIFNR) AQLIK_LIFNR
		  ,B11B_ACDOCA_BLART AQLIK_BLART
		  ,B11B_ACDOCA_BLDAT AQLIK_BLDAT
		  ,B11B_ACDOCA_BUDAT AQLIK_BUDAT
		  ,B11B_ACDOCA_RACCT AQLIK_RACCT
		  ,SUM(DBO.STRING_TO_NUMBER(B11B_ZF_ACDOCA_HSL_S)) AQLIK_HSL
		INTO DV12_01_TT_AQLIK_AP
		FROM AQLIK_AP_DETAIL AS A
		GROUP BY 
			B11B_ACDOCA_RBUKRS
			,B11B_ACDOCA_BELNR
			,DBO.REMOVE_LEADING_ZEROES(B11B_ACDOCA_LIFNR)
			,B11B_ACDOCA_BLART
			,B11B_ACDOCA_GJAHR
			,B11B_ACDOCA_BLDAT
			,B11B_ACDOCA_RACCT
			,B11B_ACDOCA_BUDAT
		

--Step 3A: adding the row number to help identify records 
--which have the same Company Code, Vendor Number,Document Number, Document Type and Posting Date
--EXEC SP_SAP_REPORT_FIELD_TO_STANDARD 'ASAP_FBL1N'
--Totals from SAP report
--Add SAP_ prefix to field names

EXEC SP_REMOVE_TABLES 'DV12_02_TT_SAP_AP'

SELECT 
		 RBUKRS SAP_RBUKRS
		,DBO.REMOVE_LEADING_ZEROES(BELNR) SAP_BELNR
		,YEAR(CONVERT(DATETIME,START_FROM_FILE_NAME,112)) SAP_GJAHR
		,DBO.REMOVE_LEADING_ZEROES(LIFNR) SAP_LIFNR
		,BLART SAP_BLART
		,BLDAT_CONVERTED SAP_BLDAT
		,BUDAT_CONVERTED SAP_BUDAT
		,RACCT SAP_RACCT
		,SUM(DBO.STRING_TO_NUMBER( HSL)) SAP_HSL

	INTO DV12_02_TT_SAP_AP
	FROM ASAP_FBL1N
	WHERE BELNR <> ''
	GROUP BY RBUKRS, BELNR, BLART, BLDAT_CONVERTED,
	DBO.REMOVE_LEADING_ZEROES(LIFNR), 
	YEAR(CONVERT(DATETIME,START_FROM_FILE_NAME,112)),	
	BUDAT_CONVERTED,
	RACCT


--Step 4/
--Compare totals from Qlik to totals from SAP


	EXECUTE SP_REMOVE_TABLES 'DV12_03_RT_COMP_AQLIK_SAP_AP'

	SELECT	
		  [AQLIK_RBUKRS]
		  ,[SAP_RBUKRS]	  
		  ,[AQLIK_BELNR]
		  ,[SAP_BELNR]
		  ,[AQLIK_LIFNR]
		  ,[SAP_LIFNR]
		  ,[AQLIK_BLART]
		  ,[SAP_BLART]
		  ,SAP_BLDAT
		  ,AQLIK_BLDAT
		  ,SAP_BUDAT
		  ,AQLIK_BUDAT
		  ,[AQLIK_GJAHR]
		  ,SAP_GJAHR
		  ,SAP_RACCT
		  ,AQLIK_RACCT
		  ,ISNULL(AQLIK_HSL, 0) AQLIK_HSL
		  ,ISNULL([SAP_HSL], 0) SAP_HSL
		  ,ISNULL(AQLIK_HSL, 0) - ISNULL(SAP_HSL, 0) AS ZF_AQLIK_MINUS_SAP_AMOUNT,
			CASE 
				WHEN	ISNULL(AQLIK_BELNR, '') <> '' AND
						ISNULL(SAP_BELNR, '') <> ''
					THEN 'Invoice in DIVA and SAP'
				WHEN	ISNULL(AQLIK_BELNR, '') <> '' AND
						ISNULL(SAP_BELNR, '') = ''
					THEN 'Invoice in DIVA not SAP'
				 WHEN	ISNULL(AQLIK_BELNR, '') = '' AND
						ISNULL(SAP_BELNR, '') <> '' 
					THEN 'Invoice in SAP not DIVA'
				ELSE	'Case not identified'
			END ZF_SUPP_MATCH_CATEGORY
	INTO	DV12_03_RT_COMP_AQLIK_SAP_AP
	FROM	DV12_01_TT_AQLIK_AP
		FULL OUTER JOIN DV12_02_TT_SAP_AP
			ON	DV12_01_TT_AQLIK_AP.AQLIK_RBUKRS	= DV12_02_TT_SAP_AP.SAP_RBUKRS
			AND	DV12_01_TT_AQLIK_AP.AQLIK_LIFNR	= DV12_02_TT_SAP_AP.SAP_LIFNR
			AND	DV12_01_TT_AQLIK_AP.AQLIK_BELNR	= DV12_02_TT_SAP_AP.SAP_BELNR
			AND DV12_01_TT_AQLIK_AP.AQLIK_BLART	= DV12_02_TT_SAP_AP.SAP_BLART
			AND DV12_01_TT_AQLIK_AP.AQLIK_GJAHR	= DV12_02_TT_SAP_AP.SAP_GJAHR
			AND DV12_01_TT_AQLIK_AP.AQLIK_RACCT = DV12_02_TT_SAP_AP.SAP_RACCT
--Step 5/
--Split the compariAPn into two tables: Enable independent filtering in QLIK

	EXEC SP_REMOVE_TABLES 'DV12_04_RT_FULL_AQLIK_AP'

	SELECT DISTINCT	AQLIK_RBUKRS
		  ,AQLIK_BELNR
		  ,AQLIK_LIFNR
		  ,AQLIK_BLART
		  ,AQLIK_BLDAT
		  ,AQLIK_BUDAT
		  ,AQLIK_RACCT
		  ,AQLIK_GJAHR
		  ,AQLIK_HSL
		  ,CONCAT(AQLIK_RBUKRS , '|' , AQLIK_BELNR + '|' , AQLIK_GJAHR)  AS ZF_KEY_JOIN_QLIK
	INTO	DV12_04_RT_FULL_AQLIK_AP
	FROM	DV12_03_RT_COMP_AQLIK_SAP_AP
	WHERE	AQLIK_BELNR IS NOT NULL
	
	EXEC SP_REMOVE_TABLES 'DV12_05_RT_FULL_SAP_AP'

	SELECT DISTINCT	SAP_RBUKRS
		  ,SAP_BELNR
		  ,SAP_BLART
		  ,SAP_LIFNR
		  ,SAP_GJAHR
		  ,SAP_BLDAT
		  ,SAP_BUDAT
		  ,SAP_RACCT
		  ,SAP_HSL
		  ,CONCAT(SAP_RBUKRS , '|' , SAP_BELNR , '|' , SAP_GJAHR) AS ZF_KEY_JOIN_SAP
	INTO	DV12_05_RT_FULL_SAP_AP

	FROM	DV12_03_RT_COMP_AQLIK_SAP_AP
	WHERE	SAP_BELNR IS NOT NULL

--Step 6/
--Split the compariAPn into two discrepancy tables: Enable independent filtering in QLIK

	EXEC SP_REMOVE_TABLES 'DV12_06_RT_DISC_AQLIK_SAP_AP'
		
	SELECT  
		  AQLIK_RBUKRS
		  ,AQLIK_BELNR
		  ,AQLIK_LIFNR
		  ,AQLIK_BLART
		  ,AQLIK_BLDAT
		  ,AQLIK_GJAHR
		  ,AQLIK_BUDAT
		  ,AQLIK_RACCT
		  ,AQLIK_HSL
		  ,ZF_AQLIK_MINUS_SAP_AMOUNT
		  ,ZF_SUPP_MATCH_CATEGORY
		  ,CONCAT(AQLIK_RBUKRS , '|' , AQLIK_BELNR , '|' , AQLIK_GJAHR)  AS ZF_KEY_JOIN_QLIK
	INTO	DV12_06_RT_DISC_AQLIK_SAP_AP
	FROM	DV12_03_RT_COMP_AQLIK_SAP_AP
	WHERE
		(ABS(ZF_AQLIK_MINUS_SAP_AMOUNT) > 10 AND ZF_SUPP_MATCH_CATEGORY = 'Invoice in DIVA and SAP')
		OR ZF_SUPP_MATCH_CATEGORY = 'Invoice in DIVA not SAP'
		

-- Discrepancy table for spend overview for SAP

	EXEC SP_REMOVE_TABLES 'DV12_07_RT_DISC_SAP_AQLIK_AP'

	SELECT	SAP_RBUKRS
		  ,SAP_BELNR
		  ,SAP_BLART
		  ,SAP_LIFNR
		  ,SAP_GJAHR
		  ,SAP_BLDAT
		  ,SAP_BUDAT
		  ,SAP_RACCT
		  ,SAP_HSL
		  ,ZF_AQLIK_MINUS_SAP_AMOUNT
		  ,ZF_SUPP_MATCH_CATEGORY
		  ,CONCAT(SAP_RBUKRS , '|' , SAP_BELNR , '|' , SAP_GJAHR) AS ZF_KEY_JOIN_SAP
	INTO	DV12_07_RT_DISC_SAP_AQLIK_AP
	FROM	DV12_03_RT_COMP_AQLIK_SAP_AP
	WHERE
		(ABS(ZF_AQLIK_MINUS_SAP_AMOUNT) > 10 AND ZF_SUPP_MATCH_CATEGORY = 'Invoice in SAP and DIVA')
		OR ZF_SUPP_MATCH_CATEGORY IN ( 'Invoice in SAP not DIVA', 'Case not identified')

		

--Step 7/
--Rename field
EXEC SP_RENAME_FIELD 'DV12_' , 'DV12_03_RT_COMP_AQLIK_SAP_AP'
EXEC SP_RENAME_FIELD 'DV12A_', 'DV12_04_RT_FULL_AQLIK_AP'
EXEC SP_RENAME_FIELD 'DV12B_', 'DV12_05_RT_FULL_SAP_AP'
EXEC SP_RENAME_FIELD 'DV12C_', 'DV12_06_RT_DISC_AQLIK_SAP_AP'
EXEC SP_RENAME_FIELD 'DV12D_', 'DV12_07_RT_DISC_SAP_AQLIK_AP'
EXEC SP_UNNAME_FIELD 'B11B_', 'DV12_08_RT_AQLIK_DETAIL'
--Delete temporary tables
EXEC SP_REMOVE_TABLES '%_TT_%'


-- Step 8/
--Send Finish Message for DIVA_BULK_IMPORT.jar: 

	INSERT INTO DV00_RT_USER_FEEDBACK_MESSAGE VALUES('', '', 'DV12 stored procedure has finished.')

/* log cube creation*/
INSERT INTO [DBO].[LOG_SP_EXECUTION] ([DATABASE],[OBJECT],[OBJECT_TYPE],[USER],[DATE],[TIME],[DESCRIPTION],[TABLE],[ROWS])
SELECT DB_NAME(),OBJECT_NAME(@@PROCID),'P',SYSTEM_USER,CONVERT(DATE,GETDATE()),CONVERT(TIME,GETDATE()),'Cube completed','DV12_03_RT_COMP_AQLIK_SAP_AP',(SELECT COUNT(*) FROM DV12_03_RT_COMP_AQLIK_SAP_AP) 
INSERT INTO [DBO].[LOG_SP_EXECUTION] ([DATABASE],[OBJECT],[OBJECT_TYPE],[USER],[DATE],[TIME],[DESCRIPTION],[TABLE],[ROWS])
SELECT DB_NAME(),OBJECT_NAME(@@PROCID),'P',SYSTEM_USER,CONVERT(DATE,GETDATE()),CONVERT(TIME,GETDATE()),'Cube completed','DV12_04_RT_FULL_AQLIK_AP',(SELECT COUNT(*) FROM DV12_04_RT_FULL_AQLIK_AP) 
INSERT INTO [DBO].[LOG_SP_EXECUTION] ([DATABASE],[OBJECT],[OBJECT_TYPE],[USER],[DATE],[TIME],[DESCRIPTION],[TABLE],[ROWS])
SELECT DB_NAME(),OBJECT_NAME(@@PROCID),'P',SYSTEM_USER,CONVERT(DATE,GETDATE()),CONVERT(TIME,GETDATE()),'Cube completed','DV12_05_RT_FULL_SAP_AP',(SELECT COUNT(*) FROM DV12_05_RT_FULL_SAP_AP) 
INSERT INTO [DBO].[LOG_SP_EXECUTION] ([DATABASE],[OBJECT],[OBJECT_TYPE],[USER],[DATE],[TIME],[DESCRIPTION],[TABLE],[ROWS])
SELECT DB_NAME(),OBJECT_NAME(@@PROCID),'P',SYSTEM_USER,CONVERT(DATE,GETDATE()),CONVERT(TIME,GETDATE()),'Cube completed','DV12_06_RT_DISC_AQLIK_SAP_AP',(SELECT COUNT(*) FROM DV12_06_RT_DISC_AQLIK_SAP_AP) 
INSERT INTO [DBO].[LOG_SP_EXECUTION] ([DATABASE],[OBJECT],[OBJECT_TYPE],[USER],[DATE],[TIME],[DESCRIPTION],[TABLE],[ROWS])
SELECT DB_NAME(),OBJECT_NAME(@@PROCID),'P',SYSTEM_USER,CONVERT(DATE,GETDATE()),CONVERT(TIME,GETDATE()),'Cube completed','DV12_07_RT_DISC_SAP_AQLIK_AP',(SELECT COUNT(*) FROM DV12_07_RT_DISC_SAP_AQLIK_AP) 

/* log end of procedure*/


INSERT INTO [DBO].[LOG_SP_EXECUTION] ([DATABASE],[OBJECT],[OBJECT_TYPE],[USER],[DATE],[TIME],[DESCRIPTION],[TABLE],[ROWS])
SELECT DB_NAME(),OBJECT_NAME(@@PROCID),'P',SYSTEM_USER,CONVERT(date,GETDATE()),CONVERT(time,GETDATE()),'Procedure finished',NULL,NULL


END 

GO
