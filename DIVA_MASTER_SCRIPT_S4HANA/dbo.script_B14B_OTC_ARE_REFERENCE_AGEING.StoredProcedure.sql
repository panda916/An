USE [DIVA_MASTER_SCRIPT_S4HANA]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

CREATE      PROC[dbo].[script_B14B_OTC_ARE_REFERENCE_AGEING]
AS
--DYNAMIC_SCRIPT_START
/*  Change history comments
	Update history 
	-------------------------------------------------------
	Date            | Who   |  Description 
	24-03-2022	    | Thuan	| Remove MANDT field in join
*/

BEGIN
    /* Initialize parameters from globals table */
    DECLARE  
				@CURRENCY NVARCHAR(3)                 = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'currency')
				,@DATE1 NVARCHAR(MAX)                           = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'date1')
				,@DATE2 NVARCHAR(MAX)                           = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'date2')
				,@DOWNLOADDATE NVARCHAR(MAX)             = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'downloaddate')
				,@DATEFORMAT VARCHAR(3)             = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'dateformat')
				,@EXCHANGERATETYPE NVARCHAR(MAX)  = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'exchangeratetype')
				,@LANGUAGE1 NVARCHAR(3)                = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'language1')
				,@LANGUAGE2 NVARCHAR(3)                = (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'language2')
				,@LIMIT_RECORDS INT                    = CAST((SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'LIMIT_RECORDS') AS INT)
				,@FISCAL_YEAR_FROM NVARCHAR(MAX)					= (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'FISCAL_YEAR_FROM')
				,@FISCAL_YEAR_TO NVARCHAR(MAX)					= (SELECT GLOBALS_VALUE FROM [AM_GLOBALS] WHERE GLOBALS_PARAMETER = 'FISCAL_YEAR_TO')
                 SET DATEFORMAT @DATEFORMAT;
 
 
    /*Test mode*/
    SET ROWCOUNT @LIMIT_RECORDS
	--------------------------------------------------------------------------------------------------------------------------
	-- step 1: Create a temp table with all month ends between @date1 and @date2
	DECLARE @STARTDATE date = (SELECT CAST(@date1 AS date))
	DECLARE @ENDDATE date = (SELECT CAST(@date2 AS date))

	EXEC SP_DROPTABLE 'B14B_00_TT_SNAPSHOT_DATE'
	CREATE TABLE B14B_00_TT_SNAPSHOT_DATE (ZF_MNTH_END date)
	WHILE @STARTDATE <= @ENDDATE
	BEGIN 
		INSERT INTO B14B_00_TT_SNAPSHOT_DATE 
		SELECT EOMONTH(@STARTDATE)

		SET @STARTDATE = DATEADD(mm, 1, @STARTDATE)
	END

    /*
        Step 1: 
            List all AR documents have an assignmet number and debit/credit indicator is debit.
    */
	EXEC SP_REMOVE_TABLES 'B14B_01_TT_AR_ASSIGMENT_DOCS'     
	SELECT DISTINCT
        B14_ACDOCA_RCLNT,
		B14_ACDOCA_RBUKRS,
		B14_ACDOCA_GJAHR,
		B14_ACDOCA_BELNR,
		B14_ACDOCA_BUZEI,
        B14_ACDOCA_DOCLN,
		B14_ACDOCA_BLDAT,
        B14_ACDOCA_ZUONR,
		B14_ZF_ACDOCA_HSL_S
	INTO B14B_01_TT_AR_ASSIGMENT_DOCS
	FROM B14_06_IT_ARE A    
	WHERE B14_BKPF_XBLNR = B14_ACDOCA_ZUONR AND ISNULL(B14_ACDOCA_ZUONR, '') <> '' AND B14_ZF_ACDOCA_DRCRK_DESC = 'Debit'


    /*
        Step 2: 
            List all documents offset value of all document we gets at step 1.
    */
    EXEC SP_REMOVE_TABLES 'B14B_02_TT_OFFSET_DOCS'
    ;WITH OFFSET_TEMP AS (
        SELECT DISTINCT
            B14_06_IT_ARE.B14_ACDOCA_RCLNT,
            B14_06_IT_ARE.B14_ACDOCA_RBUKRS,
            B14_06_IT_ARE.B14_ACDOCA_GJAHR,
            B14_06_IT_ARE.B14_ACDOCA_BELNR,
            B14_06_IT_ARE.B14_ACDOCA_BUZEI,
            B14_06_IT_ARE.B14_ACDOCA_DOCLN,
            B14_06_IT_ARE.B14_ACDOCA_ZUONR,
            DATEDIFF(DD , B14B_01_TT_AR_ASSIGMENT_DOCS.B14_ACDOCA_BLDAT, B14_06_IT_ARE.B14_ACDOCA_BLDAT) B14_ZF_OPEN_BLDAT_MINUS_OFFSET_BLDAT,
            ROW_NUMBER() OVER (
                PARTITION BY B14_06_IT_ARE.B14_ACDOCA_RBUKRS,
                             B14_06_IT_ARE.B14_ACDOCA_GJAHR,
                             B14_06_IT_ARE.B14_ACDOCA_BELNR,
                             B14_06_IT_ARE.B14_ACDOCA_BUZEI,
                             B14_06_IT_ARE.B14_ACDOCA_DOCLN
                ORDER BY B14B_01_TT_AR_ASSIGMENT_DOCS.B14_ACDOCA_BLDAT ASC
            ) ROW_ID
        FROM B14_06_IT_ARE
        INNER JOIN B14B_01_TT_AR_ASSIGMENT_DOCS
        ON B14_06_IT_ARE.B14_ACDOCA_RBUKRS = B14B_01_TT_AR_ASSIGMENT_DOCS.B14_ACDOCA_RBUKRS AND
            B14_06_IT_ARE.B14_ACDOCA_ZUONR = B14B_01_TT_AR_ASSIGMENT_DOCS.B14_ACDOCA_ZUONR AND
            B14_06_IT_ARE.B14_ACDOCA_ZUONR <> '' AND
            (B14_06_IT_ARE.B14_ZF_ACDOCA_HSL_S + B14B_01_TT_AR_ASSIGMENT_DOCS.B14_ZF_ACDOCA_HSL_S) < 0.2
    )  
    SELECT DISTINCT 
            B14_ACDOCA_RCLNT,
            B14_ACDOCA_RBUKRS,
            B14_ACDOCA_GJAHR,
            B14_ACDOCA_BELNR,
            B14_ACDOCA_BUZEI,
            B14_ACDOCA_DOCLN,
            B14_ACDOCA_ZUONR,
            B14_ZF_OPEN_BLDAT_MINUS_OFFSET_BLDAT
    INTO B14B_02_TT_OFFSET_DOCS
    FROM OFFSET_TEMP
    WHERE ROW_ID = 1


    /*
        Step 3: Combine open invoices and offset of open invoices to one table.
    */
    EXEC SP_REMOVE_TABLES 'B14B_03_TT_ASSIGMENT_DOCS_FULL'
    SELECT DISTINCT
            B14_ACDOCA_RCLNT,
            B14_ACDOCA_RBUKRS,
            B14_ACDOCA_GJAHR,
            B14_ACDOCA_BELNR,
            B14_ACDOCA_BUZEI,
            B14_ACDOCA_DOCLN,
            B14_ACDOCA_ZUONR,
            NULL B14_ZF_OPEN_BLDAT_MINUS_OFFSET_BLDAT,
            '1. Original' B14_ZF_OPEN_BLDAT_MINUS_OFFSET_BLDAT_BUCKET
    INTO B14B_03_TT_ASSIGMENT_DOCS_FULL
    FROM B14B_01_TT_AR_ASSIGMENT_DOCS
    UNION
    SELECT DISTINCT
            B14_ACDOCA_RCLNT,
            B14_ACDOCA_RBUKRS,
            B14_ACDOCA_GJAHR,
            B14_ACDOCA_BELNR,
            B14_ACDOCA_BUZEI,
            B14_ACDOCA_DOCLN,
            B14_ACDOCA_ZUONR,
            NULL,
            '2. Open'
    FROM B14B_01_TT_AR_ASSIGMENT_DOCS
    UNION
    SELECT DISTINCT
            B14_ACDOCA_RCLNT,
            B14_ACDOCA_RBUKRS,
            B14_ACDOCA_GJAHR,
            B14_ACDOCA_BELNR,
            B14_ACDOCA_BUZEI,
            B14_ACDOCA_DOCLN,
            B14_ACDOCA_ZUONR,
            B14_ZF_OPEN_BLDAT_MINUS_OFFSET_BLDAT,
            CASE          
				WHEN B14_ZF_OPEN_BLDAT_MINUS_OFFSET_BLDAT < 0 THEN '3. < 0'          
				WHEN B14_ZF_OPEN_BLDAT_MINUS_OFFSET_BLDAT > -1 AND B14_ZF_OPEN_BLDAT_MINUS_OFFSET_BLDAT < 31 THEN '4. 00 - 30'          
				WHEN B14_ZF_OPEN_BLDAT_MINUS_OFFSET_BLDAT > 30 AND B14_ZF_OPEN_BLDAT_MINUS_OFFSET_BLDAT < 61 THEN '5. 31 - 60'          
				WHEN B14_ZF_OPEN_BLDAT_MINUS_OFFSET_BLDAT > 60 AND B14_ZF_OPEN_BLDAT_MINUS_OFFSET_BLDAT < 91 THEN '6. 61 - 90'          
			    WHEN B14_ZF_OPEN_BLDAT_MINUS_OFFSET_BLDAT > 90 AND B14_ZF_OPEN_BLDAT_MINUS_OFFSET_BLDAT < 121 THEN '7. 91 - 120'          
				WHEN B14_ZF_OPEN_BLDAT_MINUS_OFFSET_BLDAT > 120 AND B14_ZF_OPEN_BLDAT_MINUS_OFFSET_BLDAT < 151 THEN '8. 121 - 150'          
				ELSE '9. > 150'
			END
    FROM B14B_02_TT_OFFSET_DOCS
    UNION
    SELECT DISTINCT
            B14_ACDOCA_RCLNT,
            B14_ACDOCA_RBUKRS,
            B14_ACDOCA_GJAHR,
            B14_ACDOCA_BELNR,
            B14_ACDOCA_BUZEI,
            B14_ACDOCA_DOCLN,
            B14_ACDOCA_ZUONR,
            NULL,
            '2. Open'
    FROM B14B_02_TT_OFFSET_DOCS
  
    /*
        Step 4: Get more information
    */
    EXEC SP_REMOVE_TABLES 'B14B_04_IT_ASSIGMENT_DOCS_DETAIL'
    SELECT DISTINCT
           B14_06_IT_ARE.B14_ACDOCA_RCLNT,
           B14_06_IT_ARE.B14_ACDOCA_RBUKRS,
           B14_06_IT_ARE.B14_ACDOCA_GJAHR,
           B14_06_IT_ARE.B14_ACDOCA_BELNR,
           B14_06_IT_ARE.B14_ACDOCA_BUZEI,
           B14_06_IT_ARE.B14_ACDOCA_DOCLN,
           B14_06_IT_ARE.B14_ACDOCA_BLDAT,
           B14_06_IT_ARE.B14_ACDOCA_BUDAT,
           B14_06_IT_ARE.B14_ACDOCA_AUGDT,
           B14_06_IT_ARE.B14_BKPF_XBLNR,
           B14_06_IT_ARE.B14_ACDOCA_ZUONR,
           CONCAT(B14_06_IT_ARE.B14_ACDOCA_RBUKRS, '-', B14_06_IT_ARE.B14_ACDOCA_BELNR, '-', B14_06_IT_ARE.B14_ACDOCA_GJAHR) ZF_ACDOCA_DOC_DETAIL,
           CONCAT(IIF(B14_06_IT_ARE.B14_ZF_ACDOCA_DRCRK_DESC = 'Debit', 'a.', 'z.') ,B14_06_IT_ARE.B14_ACDOCA_BELNR,  ' - ',  IIF(B14_06_IT_ARE.B14_ZF_ACDOCA_DRCRK_DESC = 'Debit', 'Open Invoice - ', ''), B14_06_IT_ARE.B14_T003T_LTEXT, ', ',  B14_06_IT_ARE.B14_TBSLT_LTEXT) ZF_ACDOCA_BELNR_DESC,
           B14B_03_TT_ASSIGMENT_DOCS_FULL.B14_ZF_OPEN_BLDAT_MINUS_OFFSET_BLDAT,
           B14B_03_TT_ASSIGMENT_DOCS_FULL.B14_ZF_OPEN_BLDAT_MINUS_OFFSET_BLDAT_BUCKET
        
    INTO B14B_04_IT_ASSIGMENT_DOCS_DETAIL
    FROM B14B_03_TT_ASSIGMENT_DOCS_FULL
    LEFT JOIN B14_06_IT_ARE
    ON B14_06_IT_ARE.B14_ACDOCA_RBUKRS = B14B_03_TT_ASSIGMENT_DOCS_FULL.B14_ACDOCA_RBUKRS AND
       B14_06_IT_ARE.B14_ACDOCA_GJAHR = B14B_03_TT_ASSIGMENT_DOCS_FULL.B14_ACDOCA_GJAHR AND
       B14_06_IT_ARE.B14_ACDOCA_BELNR = B14B_03_TT_ASSIGMENT_DOCS_FULL.B14_ACDOCA_BELNR AND
       B14_06_IT_ARE.B14_ACDOCA_BUZEI = B14B_03_TT_ASSIGMENT_DOCS_FULL.B14_ACDOCA_BUZEI AND
       B14_06_IT_ARE.B14_ACDOCA_DOCLN = B14B_03_TT_ASSIGMENT_DOCS_FULL.B14_ACDOCA_DOCLN


    /*
        Step 5: Create snapshot table for all AR documents with a assigment number
    */

    EXEC SP_REMOVE_TABLES 'B14B_05_IT_ASSIGMENT_DOCS_SNAPSHOT'
    SELECT DISTINCT 
            B14B_00_TT_SNAPSHOT_DATE.ZF_MNTH_END,
            B14_ACDOCA_RCLNT,
            B14_ACDOCA_RBUKRS,
            B14_ACDOCA_GJAHR,
            B14_ACDOCA_BELNR,
            B14_ACDOCA_BUZEI,
            B14_ACDOCA_DOCLN
    INTO B14B_05_IT_ASSIGMENT_DOCS_SNAPSHOT
    FROM B14B_04_IT_ASSIGMENT_DOCS_DETAIL
    LEFT JOIN B14B_00_TT_SNAPSHOT_DATE
    ON 1 = 1
    WHERE B14B_04_IT_ASSIGMENT_DOCS_DETAIL.B14_ACDOCA_BUDAT <= B14B_00_TT_SNAPSHOT_DATE.ZF_MNTH_END 
          AND (B14B_04_IT_ASSIGMENT_DOCS_DETAIL.B14_ACDOCA_AUGDT = '' OR B14B_04_IT_ASSIGMENT_DOCS_DETAIL.B14_ACDOCA_AUGDT > B14B_00_TT_SNAPSHOT_DATE.ZF_MNTH_END)

    /*
        Rename fiels for result table.
    */
    EXEC SP_UNNAME_FIELD 'B14_', 'B14B_04_IT_ASSIGMENT_DOCS_DETAIL'
    EXEC SP_UNNAME_FIELD 'B14_', 'B14B_05_IT_ASSIGMENT_DOCS_SNAPSHOT'
    EXEC SP_RENAME_FIELD 'B14B_04_', 'B14B_04_IT_ASSIGMENT_DOCS_DETAIL'
    EXEC SP_RENAME_FIELD 'B14B_05_', 'B14B_05_IT_ASSIGMENT_DOCS_SNAPSHOT'

END
GO
